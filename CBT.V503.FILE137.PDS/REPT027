000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.        REPT027.
000300
000400 AUTHOR.            BILLY FENWICK
000500
000600*  READS THE ONLINE REPORT UNDEX FILE, THE NEW INDEX
000700*  RECORDS FILE AND DELETES ALL RECORDS THAT SHOULD BE
000800*  DELETED AND CREATES A NEW INDEX FILE.
000600****************************************
000610*     (C) COPYRIGHT BILLY FENWICK
000620****************************************
000900 ENVIRONMENT DIVISION.
001000 CONFIGURATION SECTION.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300     SELECT REPORT-INDEX-FILE   ASSIGN TO DA-IDXIN
001400                         ORGANIZATION IS INDEXED
001500                         ACCESS IS SEQUENTIAL
001600                         FILE STATUS IS IDX-STATUS
001700                         RECORD KEY IS OI-KEY.
001800     SELECT NEW-REPORT-INDEX-FILE   ASSIGN TO DA-IDXNEW
001900                         ORGANIZATION IS INDEXED
002000                         ACCESS IS SEQUENTIAL
002100                         FILE STATUS IS NEW-STATUS
002200                     RECORD KEY IS NI-KEY IN NI-INDEX-RECORD.
002300     SELECT ADD-INDEX-FILE     ASSIGN TO UT-S-IDXADD.
002400     SELECT PURGE-RECORDS      ASSIGN TO UT-S-PURGED.
002500     SELECT SORT-FILE          ASSIGN TO UT-S-SORTWK1.
002600     SELECT REPORT-LIST-FILE   ASSIGN TO UT-S-REPTLST.
002700
002800 DATA DIVISION.
002900 FILE SECTION.
003000
003100 FD  NEW-REPORT-INDEX-FILE
003200     RECORD CONTAINS 57.
003300     COPY REPTB06 REPLACING ==:Z:== BY ==NI==.
003400
003500
003600 FD  PURGE-RECORDS
003700     RECORDING MODE F
003800     RECORD CONTAINS 14.
003900 01  PURGE-RECORD.
004000     03 P-REPORT-ID            PIC X(8).
004100     03 P-REPORT-DATE          PIC 9(6).
004200
004300 FD  REPORT-INDEX-FILE
004400     RECORD CONTAINS 57.
004500     COPY REPTB06 REPLACING ==:Z:== BY ==OI==.
004600
004700 FD  ADD-INDEX-FILE
004800     RECORDING MODE F
004900     RECORD CONTAINS 57.
005000     COPY REPTB06 REPLACING ==:Z:== BY ==AI==.
005100
005200 SD  SORT-FILE
005300     RECORD CONTAINS 57.
005400     COPY REPTB06 REPLACING ==:Z:== BY ==SI==.
005500
005600 FD  REPORT-LIST-FILE
005700     RECORDING MODE IS F
005800     RECORD CONTAINS 132.
005900 01  ADD-PRINT-RECORD.
006000     03 PRT-ONE                 PIC X(50).
006100     03 PRT-XYZ                 PIC X(82).
006200     03 FILLER REDEFINES PRT-XYZ.
006300        05 PRT-ID                  PIC X(8).
006400        05 FILLER                  PIC X(1).
006500        05 PRT-SID                 PIC X(8).
006600        05 FILLER                  PIC X(1).
006700        05 PRT-DATE                PIC 9(6).
006800        05 FILLER                  PIC X(1).
006900        05 PRT-BANK                PIC 9(3).
007000        05 FILLER                  PIC X(1).
007100        05 PRT-BRANCH              PIC 9(3).
007200        05 FILLER                  PIC X(1).
007300        05 PRT-SEQ                 PIC 9(7).
007400        05 FILLER                  PIC X(42).
007500      03 FILLER REDEFINES PRT-XYZ.
007600         05 PRT-NUMBER              PIC ZZZ,Z99.
007700         05 FILLER                  PIC X(75).
007800
007900 WORKING-STORAGE SECTION.
008000 77  DD-DEL-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
008100 77  DD-DEL-I-COUNT    PIC S9(7) COMP-3  VALUE ZERO.
008200 77  DD-ADD-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
008300 77  DD-TOT-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
008400 77  DD-VSM-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
008500 77  DD-WRT-I-COUNT    PIC S9(7) COMP-3  VALUE ZERO.
008600 77  DD-WRT-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
008700 77  DD-ADD-I-COUNT    PIC S9(7) COMP-3  VALUE ZERO.
008800 77  DD-OLD-I-COUNT    PIC S9(7) COMP-3  VALUE ZERO.
008900 77  DD-SEQ            PIC S9(7)         VALUE ZERO.
009000 77  I                 PIC S9(8) COMP    VALUE ZERO.
009100 77  IDX-STATUS        PIC X(2)          VALUE 'ZZ'.
009200 77  NEW-STATUS        PIC X(2)          VALUE 'ZZ'.
009300 77  GOT-IT            PIC X(1)          VALUE ' '.
009400 77  OK-OUT            PIC X(1)          VALUE ' '.
009500 77  OLD-REPORT-ID     PIC X(8)          VALUE SPACES.
009600 77  OLD-REPORT-DATE   PIC 9(6)          VALUE ZERO.
009700
009800 01  WORK-AREAS.
009900     03  PURGE-ARRAY OCCURS 3000 TIMES.
010000         05 WRK-REPORT-ID        PIC X(8).
010100         05 WRK-REPORT-DATE      PIC 9(6).
010200     COPY REPTB06 REPLACING ==:Z:== BY ==WK==.
010300
010400 LINKAGE SECTION.
010500 01  LINK-WORK-AREA.
010600     03  PARM-LENGTH   PIC S9(4) COMP.
010700     03  JOB-PARM      PIC X(5).
010800        88 DEBUG-ON  VALUE 'DEBUG'.
010900        88 REPORT-ON VALUE 'REPRT'.
011000
011100 EJECT
011200 PROCEDURE DIVISION USING LINK-WORK-AREA.
011300 0000-MAIN-ROUTINE SECTION.
011400
011500 0100-OPEN-FILES.
011600     OPEN INPUT PURGE-RECORDS, REPORT-INDEX-FILE,
011700          ADD-INDEX-FILE.
011800     OPEN OUTPUT NEW-REPORT-INDEX-FILE, REPORT-LIST-FILE.
011900
012000 0200-READ-PURGE-RECORDS.
012100     ADD 1 TO I.
012200     IF I = 3001
012300        GO TO 9300-OUT-SPACE.
012400     READ PURGE-RECORDS AT END GO TO 0900-CLOSE-PURGE-FILE.
012500     MOVE P-REPORT-ID TO  WRK-REPORT-ID (I).
012600     MOVE P-REPORT-DATE TO WRK-REPORT-DATE (I).
012700     IF REPORT-ON
012800        MOVE ALL '-' TO ADD-PRINT-RECORD
012900        WRITE ADD-PRINT-RECORD
013000              INVALID KEY GO TO 9400-PRINT-ERR
013100     END-IF.
013200     IF REPORT-ON
013300        MOVE SPACES TO ADD-PRINT-RECORD
013400        MOVE '--- TO BE DELETED=' TO PRT-ONE
013500        MOVE P-REPORT-ID TO PRT-ID
013600        MOVE P-REPORT-DATE TO PRT-DATE
013700        WRITE ADD-PRINT-RECORD
013800              INVALID KEY GO TO 9400-PRINT-ERR
013900     END-IF.
014000     GO TO 0200-READ-PURGE-RECORDS.
014100
014200 0900-CLOSE-PURGE-FILE.
014300     MOVE 'ZZZZZZZZ' TO WRK-REPORT-ID (I).
014400     MOVE 999999 TO WRK-REPORT-DATE (I).
014500     CLOSE PURGE-RECORDS.
014600     MOVE 1 TO I.
014700
014800 1000-SORT-INDEX-FILE SECTION.
014900     SORT SORT-FILE
015000        ON ASCENDING KEY SI-KEY
015100        INPUT PROCEDURE
015200          3000-START-SORT THRU 4000-END-SORT
015300        OUTPUT PROCEDURE
015400          5000-START-LOAD THRU 7000-END-LOAD.
015500
015600     IF SORT-RETURN NOT EQUAL 0
015700        DISPLAY 'REPT026 - SORT ERROR, JOB ABENDED'
015800        MOVE 16 TO RETURN-CODE.
015900     GO TO 9000-CLOSE-FILES.
016000
016100 3000-START-SORT SECTION.
016200     READ REPORT-INDEX-FILE AT END
016300        GO TO 3400-DO-SEQ-FILE.
016400     ADD 1 TO DD-VSM-COUNT.
016500     IF OI-TYPE = 'K' AND
016600        OI-REPORT-ID EQUAL WRK-REPORT-ID (I) AND
016700        OI-REPORT-DATE EQUAL WRK-REPORT-DATE (I)
016800        ADD 1 TO DD-DEL-COUNT, DD-DEL-I-COUNT
016810        PERFORM 3350-PRINT-DELETE
016900        GO TO 3000-START-SORT.
017000     IF OI-TYPE = 'K' AND
017100        (OI-REPORT-ID EQUAL OLD-REPORT-ID ) AND
017200        (OI-REPORT-DATE EQUAL OLD-REPORT-DATE )
017300        GO TO 3100-RELEASE-IT.
017400     IF OI-TYPE = 'K'
017500        GO TO 3130-CHECK-I-TYPE.
017600     GO TO 3000-START-SORT.
017700
017800 3100-RELEASE-IT.
017900     ADD 1 TO DD-TOT-COUNT.
018000     ADD 1 TO DD-OLD-I-COUNT
018100     RELEASE SI-INDEX-RECORD FROM OI-INDEX-RECORD.
018200     GO TO 3000-START-SORT.
018300
018400 3130-CHECK-I-TYPE.
018500     PERFORM 3300-FIND-I-REPORT-ID.
018600     IF GOT-IT EQUAL 'Y'
018700        ADD 1 TO DD-DEL-COUNT, DD-DEL-I-COUNT
018710        PERFORM 3350-PRINT-DELETE
018800        GO TO 3000-START-SORT.
018900     GO TO 3100-RELEASE-IT.
019000
019100 3300-FIND-I-REPORT-ID.
019200     MOVE 'N' TO OK-OUT.
019300     MOVE OI-REPORT-ID TO OLD-REPORT-ID.
019400     MOVE OI-REPORT-DATE TO OLD-REPORT-DATE.
019500     PERFORM 3310-CHECK-IT VARYING I FROM 1 BY 1
019600       UNTIL OK-OUT = 'Y'.
019700     SUBTRACT 1 FROM I.
019800
019900 3310-CHECK-IT.
020000     IF WRK-REPORT-ID (I) EQUAL OI-REPORT-ID AND
020100        WRK-REPORT-DATE (I) EQUAL OI-REPORT-DATE
020200        MOVE 'Y' TO OK-OUT, GOT-IT.
020300     IF WRK-REPORT-ID (I) EQUAL 'ZZZZZZZZ' AND
020400        WRK-REPORT-DATE (I) EQUAL 999999
020500        MOVE 'N' TO GOT-IT
020600        MOVE 'Y' TO OK-OUT.
020700     IF I EQUAL 3000
020800        MOVE 'N' TO GOT-IT
020900        MOVE 'Y' TO OK-OUT.
021000
021002 3350-PRINT-DELETE.
021010     IF REPORT-ON
021020        MOVE SPACES TO ADD-PRINT-RECORD
021030        MOVE '-  RECORD DELETED=' TO PRT-ONE
021040        MOVE OI-REPORT-ID TO PRT-ID
021041        MOVE OI-REPORT-SID TO PRT-SID
021050        MOVE OI-REPORT-DATE TO PRT-DATE
021060        WRITE ADD-PRINT-RECORD
021080     END-IF.
021090
021100 3400-DO-SEQ-FILE.
021200     CLOSE REPORT-INDEX-FILE.
021300
021400 3500-READ-SEQ-FILE.
021500     READ ADD-INDEX-FILE AT END
021600        GO TO 3900-CLOSE-ADD-FILE.
021700     ADD 1 TO DD-ADD-COUNT.
021800     ADD 1 TO DD-TOT-COUNT.
021900     PERFORM 3600-RELEASE.
022000     GO TO 3500-READ-SEQ-FILE.
022100
022200 3600-RELEASE.
022300     ADD 1 TO DD-ADD-I-COUNT.
022400     RELEASE SI-INDEX-RECORD FROM AI-INDEX-RECORD.
022500
022600 3900-CLOSE-ADD-FILE.
022700     CLOSE ADD-INDEX-FILE.
022800
022900 4000-END-SORT. EXIT.
023000
023100 5000-START-LOAD SECTION.
023200     RETURN SORT-FILE RECORD INTO WK-INDEX-RECORD
023300         AT END GO TO 6900-CLOSE-FILE.
023400     ADD 1 TO DD-WRT-COUNT.
023500     PERFORM 5200-LOAD-FILE.
023600     GO TO 5000-START-LOAD.
023700
023800 5200-LOAD-FILE.
023900     ADD 1 TO DD-WRT-I-COUNT
024000     MOVE WK-INDEX-RECORD TO NI-INDEX-RECORD
024100     WRITE NI-INDEX-RECORD INVALID KEY GO TO 5298-ERROR.
024200     IF NEW-STATUS NOT EQUAL ZERO
024300        GO TO 5298-ERROR.
024400
024500 5298-ERROR.
024600     DISPLAY '---ERROR WRITING TO OUTPUT VSAM FILE-----------'.
024700     DISPLAY '*****START OF RECORD*********'.
024800     DISPLAY WK-INDEX-RECORD.
024900     DISPLAY 'VSAM STATUS=' NEW-STATUS.
025000     DISPLAY '*****END OF RECORD*********'.
025100     MOVE 16 TO RETURN-CODE.
025200
025300 6900-CLOSE-FILE.
025400     CLOSE NEW-REPORT-INDEX-FILE.
025500
025600 7000-END-LOAD. EXIT.
025700
025800 9000-CLOSE-FILES SECTION.
025900     MOVE ALL '-' TO ADD-PRINT-RECORD.
026000     WRITE ADD-PRINT-RECORD.
026100     MOVE SPACES TO ADD-PRINT-RECORD.
026200     MOVE ' TOTAL VSAM INDEX RECORDS READ IN  ' TO PRT-ONE.
026300     MOVE DD-VSM-COUNT TO PRT-NUMBER.
026400     WRITE ADD-PRINT-RECORD.
026500     MOVE ALL '-' TO ADD-PRINT-RECORD.
026600     WRITE ADD-PRINT-RECORD.
026610     MOVE SPACES TO ADD-PRINT-RECORD.
026700     MOVE '   INDEX RECORDS KEPT FROM INPUT FILE ' TO PRT-ONE.
026800     MOVE DD-OLD-I-COUNT TO PRT-NUMBER.
026900     WRITE ADD-PRINT-RECORD.
027000     MOVE ALL '-' TO ADD-PRINT-RECORD.
027100     WRITE ADD-PRINT-RECORD.
027110     MOVE SPACES TO ADD-PRINT-RECORD.
027200     MOVE '   INDEX RECORDS DELETED.' TO PRT-ONE.
027300     MOVE DD-DEL-I-COUNT TO PRT-NUMBER.
027400     WRITE ADD-PRINT-RECORD.
027500     MOVE ALL '-' TO ADD-PRINT-RECORD.
027600     WRITE ADD-PRINT-RECORD.
027610     MOVE SPACES TO ADD-PRINT-RECORD.
027700     MOVE '   INDEX RECORDS ADDED.' TO PRT-ONE.
027800     MOVE DD-ADD-I-COUNT TO PRT-NUMBER.
027900     WRITE ADD-PRINT-RECORD.
028000     MOVE ALL '-' TO ADD-PRINT-RECORD.
028100     WRITE ADD-PRINT-RECORD.
028110     MOVE SPACES TO ADD-PRINT-RECORD.
028200     MOVE ' TOTAL INDEX RECORDS WRITTEN OUT.' TO PRT-ONE.
028300     MOVE DD-WRT-I-COUNT TO PRT-NUMBER.
028400     WRITE ADD-PRINT-RECORD.
028500     MOVE ALL '-' TO ADD-PRINT-RECORD.
028600     WRITE ADD-PRINT-RECORD.
028700     CLOSE REPORT-LIST-FILE.
028800     GO TO 9999-END-OF-PROGRAM.
028900
029000 9300-OUT-SPACE.
029100     DISPLAY '*** OUT OF SPACE IN PURGE ARRAY**'.
029200     MOVE 16 TO RETURN-CODE .
029300     GO TO 9000-CLOSE-FILES.
029400
029500 9400-PRINT-ERR.
029600     DISPLAY '*** ERROR WRITING TO PRINT-LIST-FILE  ***'.
029700     MOVE 16 TO RETURN-CODE.
029800     GO TO 9999-END-OF-PROGRAM.
029900
030000 9999-END-OF-PROGRAM.
030100     STOP RUN.
030200
