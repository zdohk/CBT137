000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.        REPT128.
000300
000400 AUTHOR.            BILLY FENWICK
000500*
000600*  THIS PROGRAMS READS THE PURGED RECORDS FILE AND DELETES
000700*  ALL OF THE MATCHING TYPE "T" HISTORY RECORDS
000800*  FROM THE ONLINE HISTORY FILE.
000900*
000600****************************************
000610*     (C) COPYRIGHT BILLY FENWICK
000620****************************************
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 INPUT-OUTPUT SECTION.
001300 FILE-CONTROL.
001400     SELECT REPORT-HISTORY-FILE   ASSIGN TO DA-HSTIN
001500                         ORGANIZATION IS INDEXED
001600                         ACCESS IS DYNAMIC
001700                         FILE STATUS IS HST-STATUS
001800                         RECORD KEY IS T-KEY.
001900     SELECT PURGE-RECORDS    ASSIGN TO UT-S-PURGED.
002000     SELECT REPORT-LIST-FILE   ASSIGN TO UT-S-REPTLST.
002100
002200 DATA DIVISION.
002300 FILE SECTION.
002400
002500 FD  PURGE-RECORDS
002600     BLOCK CONTAINS 0 RECORDS
002700     RECORDING MODE F
002800     RECORD CONTAINS 14.
002900 01  PURGE-D-RECORD.
003000     03 PT-REPORT-ID            PIC X(8).
003200     03 PT-REPORT-DATE          PIC 9(6).
003300
003400 FD  REPORT-HISTORY-FILE
003500     RECORD CONTAINS 100.
003600     COPY REPTB07 REPLACING ==:Z:== BY ==T==.
003700
003800 FD  REPORT-LIST-FILE
003900     RECORDING MODE IS F
004000     RECORD CONTAINS 132.
004100 01  ADD-PRINT-RECORD.
004200     03 PRT-ONE                 PIC X(50).
004300     03 PRT-XYZ                 PIC X(82).
004400     03 X-FIL REDEFINES PRT-XYZ.
004500        05 PRT-ID                  PIC X(8).
004600        05 FILLER                  PIC X(1).
004700        05 PRT-SID                 PIC X(8).
004800        05 FILLER                  PIC X(1).
004900        05 PRT-DATE                PIC 9(6).
005000        05 FILLER                  PIC X(1).
005100        05 PRT-BANK                PIC 9(3).
005200        05 FILLER                  PIC X(1).
005300        05 PRT-BRANCH              PIC 9(3).
005400        05 FILLER                  PIC X(1).
005500        05 PRT-SEQ                 PIC 9(7).
005600        05 FILLER                  PIC X(42).
005700     03 X-FILL REDEFINES PRT-XYZ.
005800        05 PRT-NUMBER              PIC ZZZ,Z99.
005900        05 FILLER                  PIC X(75).
006000
006100 WORKING-STORAGE SECTION.
006200 77  TT-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
006300 77  SUB           PIC S9(4) COMP    VALUE ZERO.
006400 77  II-SEQ        PIC S9(7)         VALUE ZERO.
006410 77  ARRAY-VALUE   PIC S9(7)         VALUE +300.
006500 77  HST-STATUS    PIC X(2)          VALUE 'ZZ'.
006600
006620 01  PURGE-WORK-AREA.
006630     03  PURGE-KEY  OCCURS 300 TIMES.
006640         05  PURGE-ID    PIC  X(8).
006650         05  PURGE-DATE  PIC  S9(6).
006700 EJECT
006800 LINKAGE SECTION.
006900 01  LINK-WORK-AREA.
007000     03  PARM-LENGTH   PIC S9(4) COMP.
007100     03  JOB-PARM      PIC X(5).
007200        88 DEBUG-ON  VALUE 'DEBUG'.
007300        88 REPORT-ON VALUE 'REPRT'.
007400
007500 EJECT
007600 PROCEDURE DIVISION USING LINK-WORK-AREA.
007700 0000-MAIN-ROUTINE.
007800
007900     OPEN INPUT PURGE-RECORDS
008000          I-O REPORT-HISTORY-FILE.
008100     OPEN OUTPUT REPORT-LIST-FILE.
008200
008300 4100-READ-PURGE-FILES.
008400     READ PURGE-RECORDS AT END GO TO 5000-FIND-T-RECORDS.
008500     IF DEBUG-ON
008600        DISPLAY '4000-READ-PURGE-RECORD'
008700        DISPLAY '   PT-REPORT-ID=' PT-REPORT-ID
008900        DISPLAY ' PT-REPORT-DATE=' PT-REPORT-DATE.
008910     ADD 1 TO SUB.
008920     IF SUB = ARRAY-VALUE
008930       GO TO 9307-BAD-SUB.
008990     MOVE PT-REPORT-ID TO PURGE-ID (SUB).
008991     MOVE PT-REPORT-DATE TO PURGE-DATE (SUB).
008992     GO TO 4100-READ-PURGE-FILES.
009000
009100 5000-FIND-T-RECORDS.
009200     MOVE LOW-VALUES TO T-HISTORY-RECORD.
009300     MOVE 'T' TO T-TYPE.
009800     START REPORT-HISTORY-FILE KEY IS GREATER THAN T-KEY
009900        INVALID KEY GO TO 9300-BAD-START.
010000     IF HST-STATUS NOT EQUAL ZERO
010100         GO TO 9300-BAD-START.
010200
010300 5050-READ-HISTORY-FILE.
010400     READ REPORT-HISTORY-FILE NEXT RECORD AT END
010500        GO TO 9000-CLOSE-FILES.
010510     MOVE ZERO TO SUB.
010600     IF DEBUG-ON
010700        DISPLAY '3150-READ-INDEX-FILE'
010800        DISPLAY '  T-REPORT-ID= ' T-REPORT-ID
010900        DISPLAY '  T-REPORT-SID= ' T-REPORT-SID
011000        DISPLAY '  T-REPORT-DATE= ' T-REPORT-DATE
011100        DISPLAY '  T-BANK= ' T-BANK
011200        DISPLAY '  T-BRANCH= ' T-BRANCH
011300     END-IF.
011700
011800 5150-CHECK-DATE.
011900     ADD 1 TO SUB.
012000     IF PURGE-ID (SUB) = LOW-VALUES
012100       GO TO 5050-READ-HISTORY-FILE.
012200     IF (T-REPORT-ID EQUAL PURGE-ID (SUB)) AND
012210        (T-REPORT-DATE EQUAL PURGE-DATE (SUB) )
012211        IF DEBUG-ON
012212           DISPLAY '5150-CHECK-DATE'
012213           DISPLAY '  HIT T-REPORT-ID=' T-REPORT-ID
012214           DISPLAY '  HIT T-REPORT-DATE=' T-REPORT-DATE
012215        END-IF
012220        GO TO 5200-DELETE-RECORD.
012230     GO TO 5150-CHECK-DATE.
012400
012500 5200-DELETE-RECORD.
012600     IF DEBUG-ON
012700        DISPLAY ' TYPE -T- HISTORY RECORD = '
012800               T-REPORT-DATE ' ' T-REPORT-ID ' '
012900               T-REPORT-SID.
013000     ADD 1 TO TT-COUNT.
013100     IF REPORT-ON
013200        MOVE SPACES TO ADD-PRINT-RECORD
013300        MOVE '  RECORD DELETED ---' TO PRT-ONE
013400        MOVE T-REPORT-ID TO PRT-ID
013500        MOVE T-REPORT-SID TO PRT-SID
013600        MOVE T-REPORT-DATE TO PRT-DATE
013700        MOVE T-BANK TO PRT-BANK
013800        MOVE T-BRANCH TO PRT-BRANCH
013900        WRITE ADD-PRINT-RECORD
014000     END-IF.
014100     DELETE REPORT-HISTORY-FILE RECORD   INVALID KEY
014200        GO TO 9920-HISTORY-FILE.
014300     IF HST-STATUS NOT EQUAL ZERO
014400       GO TO 9920-HISTORY-FILE.
014500     GO TO 5050-READ-HISTORY-FILE.
014600
014700 9000-CLOSE-FILES.
014800     IF REPORT-ON
014900        MOVE ALL '-' TO ADD-PRINT-RECORD
015000        WRITE ADD-PRINT-RECORD
015100        MOVE SPACES TO ADD-PRINT-RECORD
015200      MOVE ' TOTAL HISTORY TYPE "T" RECORDS DELETED -' TO PRT-ONE
015300        MOVE TT-COUNT TO PRT-NUMBER
015400        WRITE ADD-PRINT-RECORD
015500        MOVE ALL '-' TO ADD-PRINT-RECORD
015600        WRITE ADD-PRINT-RECORD
015700     END-IF.
015800     CLOSE PURGE-RECORDS
015900           REPORT-HISTORY-FILE
016000           REPORT-LIST-FILE.
016100     GO TO 9999-END-OF-PROGRAM.
016200
016300 9300-BAD-START.
016400     DISPLAY '*** INVALID START BROWSE ON REPORT-HISTRORY-FILE**'.
016500     MOVE 16 TO RETURN-CODE
016600     DISPLAY 'VSAM STATUS CODE=' HST-STATUS.
016700     GO TO 9000-CLOSE-FILES.
016710
016720 9307-BAD-SUB.
016730     DISPLAY '*** OUT OF ARRAY SPACE**'.
016740     DISPLAY '*** CHANGE ARRAY SIZE IN PROGRAM **'.
016750     MOVE 16 TO RETURN-CODE
016760     GO TO 9000-CLOSE-FILES.
016800
016900 9920-HISTORY-FILE.
017000     DISPLAY '*** WRITE ERROR ON REPORT-HISTORY-FILE ***'.
017100     MOVE 14 TO RETURN-CODE.
017200     DISPLAY 'VSAM STATUS CODE=' HST-STATUS.
017300     GO TO 9000-CLOSE-FILES.
017400
017500 9999-END-OF-PROGRAM.
017600     STOP RUN.
