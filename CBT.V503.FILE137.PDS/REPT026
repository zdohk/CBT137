000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.        REPT026.
000300
000400 AUTHOR.            BILLY FENWICK
000500
000600*  READS THE ONLINE REPORT DETAIL FILE, THE NEW DETAIL
000700*  RECORDS FILE AND DELETES ALL RECORDS THAT SHOULD BE
000800*  DELETED AND CREATES A NEW DETAIL FILE.
000600****************************************
000610*     (C) COPYRIGHT BILLY FENWICK
000620****************************************
000900 ENVIRONMENT DIVISION.
001000 CONFIGURATION SECTION.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300     SELECT REPORT-DETAIL-FILE  ASSIGN TO DA-DTLIN
001400                         ORGANIZATION IS INDEXED
001500                         ACCESS IS SEQUENTIAL
001600                         FILE STATUS IS DTL-STATUS
001700                         RECORD KEY IS D-KEY.
001800     SELECT NEW-REPORT-DETAIL-FILE  ASSIGN TO DA-DTLNEW
001900                         ORGANIZATION IS INDEXED
002000                         ACCESS IS SEQUENTIAL
002100                         FILE STATUS IS NEW-STATUS
002200                         RECORD KEY IS N-KEY.
002300     SELECT ADD-DETAIL-FILE    ASSIGN TO UT-S-DTLADD.
002400     SELECT PURGE-RECORDS      ASSIGN TO UT-S-PURGED.
002500     SELECT SORT-FILE          ASSIGN TO UT-S-SORTWK1.
002600     SELECT REPORT-LIST-FILE   ASSIGN TO UT-S-REPTLST.
002700
002800 DATA DIVISION.
002900 FILE SECTION.
003000
003100 FD  NEW-REPORT-DETAIL-FILE
003200     RECORD IS VARYING FROM 153 TO 11724                                08
003300       DEPENDING ON N-LENGTH.                                           08
003400     COPY REPTB05 REPLACING ==:Z:== BY ==N==.
003500
003600 SD  SORT-FILE
003700     RECORD IS VARYING FROM 153 TO 11724                                08
003800       DEPENDING ON S-LENGTH.                                           08
003900     COPY REPTB05 REPLACING ==:Z:== BY ==S==.
004000
004100 FD  PURGE-RECORDS
004200     BLOCK CONTAINS 0 RECORDS
004300     RECORDING MODE F
004400     LABEL RECORDS ARE STANDARD
004500     RECORD CONTAINS 14.
004600 01  PURGE-RECORD.
004700     03 P-REPORT-ID            PIC X(8).
004800     03 P-REPORT-DATE          PIC 9(6).
004900
005000 FD  REPORT-DETAIL-FILE
005100     RECORD IS VARYING FROM 153 TO 11724                                08
005200       DEPENDING ON D-LENGTH.                                           08
005300     COPY REPTB05 REPLACING ==:Z:== BY ==D==.
005400
005500 FD  ADD-DETAIL-FILE
005600     RECORDING MODE IS V
005700     RECORD IS VARYING FROM 153 TO 11724                                08
005800       DEPENDING ON A-LENGTH.                                           08
005900     COPY REPTB05 REPLACING ==:Z:== BY ==A==.
006000
006100 FD  REPORT-LIST-FILE
006200     RECORDING MODE IS F
006300     RECORD CONTAINS 132.
006400 01  ADD-PRINT-RECORD.
006500     03 PRT-ONE                 PIC X(50).
006600     03 PRT-XYZ                 PIC X(82).
006700     03 X-FIL REDEFINES PRT-XYZ.
006800        05 PRT-ID                  PIC X(8).
006900        05 FILLER                  PIC X(1).
007000        05 PRT-DATE                PIC 9(6).
007100        05 FILLER                  PIC X(1).
007200        05 PRT-SEQ                 PIC 9(7).
007300        05 FILLER                  PIC X(59).
007400     03 X-FILL REDEFINES PRT-XYZ.
007500        05 PRT-NUMBER              PIC ZZZ,Z99.
007600        05 FILLER                  PIC X(75).
007700
007800 WORKING-STORAGE SECTION.
007900 77  PURGE-ARRAY-COUNT PIC S9(8) COMP    VALUE 3000.
008000 77  D-LENGTH          PIC 9(4)  COMP    VALUE ZERO.
008100 77  D-LEN             PIC 9(4)  COMP    VALUE ZERO.
008200 77  A-LENGTH          PIC 9(4)  COMP    VALUE ZERO.
008300 77  A-LEN             PIC 9(4)  COMP    VALUE ZERO.
008400 77  S-LENGTH          PIC 9(4)  COMP    VALUE ZERO.
008500 77  S-LEN             PIC 9(4)  COMP    VALUE ZERO.
008600 77  N-LENGTH          PIC 9(4)  COMP    VALUE ZERO.
008700 77  N-LEN             PIC 9(4)  COMP    VALUE ZERO.
008800 77  W-LENGTH          PIC 9(4)  COMP    VALUE ZERO.
008900 77  W-LEN             PIC 9(4)  COMP    VALUE ZERO.
009000 77  DD-DEL-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
009100 77  DD-ADD-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
009200 77  DD-TOT-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
009300 77  DD-VSM-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
009400 77  DD-WRT-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
009500 77  DD-SEQ            PIC S9(7)         VALUE ZERO.
009600 77  AI-COUNT          PIC 9(5)  COMP-3  VALUE ZERO.                    00
009700 77  DI-COUNT          PIC 9(5)  COMP-3  VALUE ZERO.                    00
009800 77  NI-COUNT          PIC 9(5)  COMP-3  VALUE ZERO.                    00
009900 77  SI-COUNT          PIC 9(5)  COMP-3  VALUE ZERO.                    00
010000 77  WI-COUNT          PIC 9(5)  COMP-3  VALUE ZERO.                    00
010100 77  I                 PIC S9(8) COMP    VALUE ZERO.
010200 77  DTL-STATUS        PIC X(2)          VALUE 'ZZ'.
010300 77  NEW-STATUS        PIC X(2)          VALUE 'ZZ'.
010400 77  DEL-IT            PIC X(1)          VALUE ' '.
010500 77  OK-OUT            PIC X(1)          VALUE ' '.
010600 77  OLD-REPORT-ID     PIC X(8)          VALUE SPACES.
010700 77  OLD-REPORT-DATE   PIC 9(6)          VALUE ZERO.
010800
010900 01  WORK-AREAS.
011000     03  PURGE-ARRAY OCCURS 3000 TIMES.
011100         05 WRK-REPORT-ID        PIC X(8).
011200         05 WRK-REPORT-DATE      PIC 9(6).
011300
011400     COPY REPTB05 REPLACING ==:Z:== BY ==W==.
011500
011600 LINKAGE SECTION.
011700 01  LINK-WORK-AREA.
011800     03  PARM-LENGTH   PIC S9(4) COMP.
011900     03  JOB-PARM      PIC X(5).
012000        88 DEBUG-ON  VALUE 'DEBUG'.
012100        88 REPORT-ON VALUE 'REPRT'.
012200
012300 EJECT
012400 PROCEDURE DIVISION USING LINK-WORK-AREA.
012500 0000-MAIN-ROUTINE SECTION.
012600
012700 0100-OPEN-FILES.
012800     OPEN INPUT PURGE-RECORDS, REPORT-DETAIL-FILE,
012900          ADD-DETAIL-FILE.
013000     OPEN OUTPUT NEW-REPORT-DETAIL-FILE.
013100     OPEN OUTPUT REPORT-LIST-FILE.
013200     IF PARM-LENGTH GREATER THAN 0
013300        DISPLAY 'JOB-PARM=' JOB-PARM.
013400
013500 0200-READ-PURGE-RECORDS.
013600     IF DEBUG-ON
013700        DISPLAY '0200-READ-PURGE-RECORDS'.
013800     ADD 1 TO I.
013900     IF I = PURGE-ARRAY-COUNT
014000        GO TO 9300-OUT-SPACE.
014100     READ PURGE-RECORDS AT END GO TO 0900-CLOSE-PURGE-FILE.
014200     IF REPORT-ON
014300        MOVE ALL '-' TO ADD-PRINT-RECORD
014400        WRITE ADD-PRINT-RECORD
014500              INVALID KEY GO TO 9400-PRINT-ERR
014600     END-IF.
014700     IF REPORT-ON
014800        MOVE SPACES TO ADD-PRINT-RECORD
014900        MOVE '--- TO BE DELETED=' TO PRT-ONE
015000        MOVE P-REPORT-ID TO PRT-ID
015100        MOVE P-REPORT-DATE TO PRT-DATE
015200        WRITE ADD-PRINT-RECORD
015300              INVALID KEY GO TO 9400-PRINT-ERR
015400     END-IF.
015500     MOVE P-REPORT-ID TO  WRK-REPORT-ID (I).
015600     MOVE P-REPORT-DATE TO WRK-REPORT-DATE (I).
015700     GO TO 0200-READ-PURGE-RECORDS.
015800
015900 0900-CLOSE-PURGE-FILE.
016000     IF DEBUG-ON
016100       DISPLAY '0900-CLOSE-PURGE-FILE'.
016200     ADD 1 TO I.
016300     MOVE 'ZZZZZZZZ' TO WRK-REPORT-ID (I).
016400     MOVE 999999 TO WRK-REPORT-DATE (I).
016500     CLOSE PURGE-RECORDS.
016600     MOVE 1 TO I.
016700
016800 1000-SORT-DETAIL-FILE SECTION.
016900     IF DEBUG-ON
017000       DISPLAY '1000-SORT-DETAIL-FILE'.
017100     SORT SORT-FILE
017200        ON ASCENDING KEY S-KEY IN S-RECORD
017300        INPUT PROCEDURE
017400          3000-START-SORT THRU 4000-END-SORT
017500        OUTPUT PROCEDURE
017600          5000-START-LOAD THRU 7000-END-LOAD.
017700
017800     IF SORT-RETURN NOT EQUAL 0
017900        DISPLAY 'REPT026 - SORT ERROR, JOB ABENDED'
018000        MOVE 16 TO RETURN-CODE.
018100     GO TO 9000-CLOSE-FILES.
018200
018300 3000-START-SORT SECTION.
018400     IF DEBUG-ON
018500       DISPLAY '3000-START-SORT SECTION'.
018600     READ REPORT-DETAIL-FILE AT END
018700        GO TO 3400-DO-SEQ-FILE.
018800     ADD 1 TO DD-VSM-COUNT.
018900     PERFORM 3300-FIND-REPORT-ID.
019000     IF DEL-IT EQUAL 'Y'
019100        ADD 1 TO DD-DEL-COUNT
019200        IF DEBUG-ON
019300          DISPLAY '--DELETING =' D-REPORT-ID '/' D-REPORT-DATE '/' D-SEQ
019400        END-IF
019500        IF REPORT-ON
019600           MOVE SPACES TO PRT-ONE
019700           MOVE '--- DELETING =' TO PRT-ONE
019800           MOVE D-REPORT-ID TO PRT-ID
019900           MOVE D-REPORT-DATE TO PRT-DATE
020000           MOVE D-SEQ TO PRT-SEQ
020100           WRITE ADD-PRINT-RECORD
020200              INVALID KEY GO TO 9400-PRINT-ERR
020300        END-IF
020400        GO TO 3000-START-SORT.
020500
020600 3100-RELEASE-IT.
020700     IF DEBUG-ON
020800       DISPLAY '3100-RELEASE-IT'.
020900     ADD 1 TO DD-TOT-COUNT.
021000     COMPUTE S-LENGTH = (D-NUMBER-OF-LINES * 133) + 20.
021100     MOVE D-REPORT-ID TO S-REPORT-ID.
021200     MOVE D-REPORT-DATE TO S-REPORT-DATE.
021300     MOVE D-SEQ TO S-SEQ.
021400     MOVE D-NUMBER-OF-LINES TO S-NUMBER-OF-LINES, SI-COUNT.
021500     MOVE D-NUMBER-OF-LINES TO DI-COUNT.
021600     PERFORM 3210-MOVE-DATA
021700             VARYING I FROM 1 BY 1
021800             UNTIL I > DI-COUNT.
021900     PERFORM 3200-RELEASE THRU 3299-EXIT.
022000     GO TO 3000-START-SORT.
022100
022200 3210-MOVE-DATA.
022300     MOVE D-LINES(I) TO S-LINES(I).
022400
022500 3200-RELEASE.
022600     IF DEBUG-ON
022700       DISPLAY '3200-RELEASE'
022800       DISPLAY 'D-RECL=' LENGTH OF D-RECORD
022900       DISPLAY 'D-LENGTH=' D-LENGTH
023000       DISPLAY 'S-RECL=' LENGTH OF S-RECORD
023100       DISPLAY 'S-LENGTH=' S-LENGTH
023200       DISPLAY 'D-NUMBER-OF-LINES=' D-NUMBER-OF-LINES
023300       DISPLAY 'S-NUMBER-OF-LINES=' S-NUMBER-OF-LINES
023400       DISPLAY 'D-RECORD=' D-RECORD
023500       DISPLAY 'S-RECORD=' S-RECORD.
023600     RELEASE S-RECORD.
023700
023800 3299-EXIT. EXIT.
023900
024000 3300-FIND-REPORT-ID.
024100     IF DEBUG-ON
024200       DISPLAY '3300-FIND-REPORT-ID'.
024300     MOVE 'N' TO OK-OUT, DEL-IT.
024400     PERFORM 3310-CHECK-IT VARYING I FROM 1 BY 1
024500       UNTIL OK-OUT = 'Y'.
024600     SUBTRACT 1 FROM I.
024700
024800 3310-CHECK-IT.
024900     IF DEBUG-ON
025000       DISPLAY '3310-CHECK-IT'.
025100     IF WRK-REPORT-ID (I) EQUAL D-REPORT-ID AND
025200        WRK-REPORT-DATE (I) EQUAL D-REPORT-DATE
025300        MOVE 'Y' TO OK-OUT, DEL-IT.
025400     IF WRK-REPORT-ID (I) EQUAL 'ZZZZZZZZ' AND
025500        WRK-REPORT-DATE (I) EQUAL 999999
025600        MOVE 'N' TO DEL-IT
025700        MOVE 'Y' TO OK-OUT.
025800     IF I EQUAL PURGE-ARRAY-COUNT
025900        MOVE 'N' TO DEL-IT
026000        MOVE 'Y' TO OK-OUT.
026100
026200 3400-DO-SEQ-FILE.
026300     IF DEBUG-ON
026400       DISPLAY '3400-DO-SEQ-FILE'.
026500     CLOSE REPORT-DETAIL-FILE.
026600
026700 3500-READ-SEQ-FILE.
026800     IF DEBUG-ON
026900        DISPLAY '3500-READ-SEQ-FILE'.
027000     READ ADD-DETAIL-FILE AT END
027100        GO TO 3900-CLOSE-ADD-FILE.
027200     ADD 1 TO DD-ADD-COUNT.
027300     ADD 1 TO DD-TOT-COUNT.
027400     MOVE A-LENGTH TO S-LENGTH.
027500     MOVE A-REPORT-ID TO S-REPORT-ID.
027600     MOVE A-REPORT-DATE TO S-REPORT-DATE.
027700     MOVE A-SEQ TO S-SEQ.
027800     MOVE A-NUMBER-OF-LINES TO SI-COUNT, AI-COUNT.
027900     MOVE A-NUMBER-OF-LINES TO S-NUMBER-OF-LINES.
028000     PERFORM 3510-MOVE-DATA
028100             VARYING I FROM 1 BY 1
028200             UNTIL I > AI-COUNT.
028300     PERFORM 3600-RELEASE THRU 3699-EXIT.
028400     GO TO 3500-READ-SEQ-FILE.
028500
028600 3510-MOVE-DATA.
028700     MOVE A-LINES(I) TO S-LINES(I).
028800
028900 3600-RELEASE.
029000     IF DEBUG-ON
029100       DISPLAY '3600-RELEASE'
029200       DISPLAY 'A-RECL=' LENGTH OF A-RECORD
029300       DISPLAY 'A-LENGTH=' A-LENGTH
029400       DISPLAY 'S-RECL=' LENGTH OF S-RECORD
029500       DISPLAY 'S-LENGTH=' S-LENGTH
029600       DISPLAY 'A-NUMBER-OF-LINES=' A-NUMBER-OF-LINES
029700       DISPLAY 'S-NUMBER-OF-LINES=' S-NUMBER-OF-LINES
029800       DISPLAY 'A-RECORD=' A-RECORD
029900       DISPLAY 'S-RECORD=' S-RECORD.
030000*    RELEASE S-RECORD FROM A-RECORD.
030100     RELEASE S-RECORD.
030200     GO TO 3699-EXIT.
030300
030400 3699-EXIT. EXIT.
030500
030600 3900-CLOSE-ADD-FILE.
030700     IF DEBUG-ON
030800       DISPLAY '3900-CLOSE-ADD-FILE'.
030900     CLOSE ADD-DETAIL-FILE.
031000
031100 4000-END-SORT. EXIT.
031200
031300 5000-START-LOAD SECTION.
031400     IF DEBUG-ON
031500       DISPLAY '5000-START-LOAD'.
031600     RETURN SORT-FILE RECORD
031700         AT END GO TO 6900-CLOSE-FILE.
031800     MOVE S-LENGTH TO S-LEN.
031900     MOVE S-RECORD(1:S-LEN) TO N-RECORD(1:S-LEN).
031910     ADD 1 TO DD-WRT-COUNT.
032000     IF DEBUG-ON
032100       DISPLAY '  DD-WRT-COUNT=' DD-WRT-COUNT
032110       DISPLAY 'S-LENGTH=' S-LENGTH
032200       DISPLAY 'S-LEN=' S-LEN
032210       DISPLAY 'S-KEY=' S-REPORT-ID, S-REPORT-DATE, S-SEQ
032220       DISPLAY 'N-KEY=' N-REPORT-ID, N-REPORT-DATE, N-SEQ.
032600     PERFORM 5200-LOAD-FILE THRU 5299-EXIT.
032700     GO TO 5000-START-LOAD.
032800
032900 5200-LOAD-FILE.
033000     IF DEBUG-ON
033100        DISPLAY '5200-LOAD-FILE'.
033200     COMPUTE N-LENGTH = (S-NUMBER-OF-LINES * 133) + 20.
033300     MOVE S-NUMBER-OF-LINES TO NI-COUNT.
033400*    DISPLAY 'DD-WRT-COUNT=' DD-WRT-COUNT.
033500     IF DEBUG-ON
033600       DISPLAY 'DD-WRT-COUNT=' DD-WRT-COUNT
033700       DISPLAY 'S-RECL=' LENGTH OF W-RECORD
033800       DISPLAY 'N-RECL=' LENGTH OF N-RECORD
033900       DISPLAY 'N-LENGTH=' N-LENGTH
034000       DISPLAY 'S-NUMBER-OF-LINES=' S-NUMBER-OF-LINES
034100       DISPLAY 'N-NUMBER-OF-LINES=' N-NUMBER-OF-LINES
034200       DISPLAY 'S-KEY=' S-REPORT-ID, S-REPORT-DATE, S-SEQ
034300       DISPLAY 'N-KEY=' N-REPORT-ID, N-REPORT-DATE, N-SEQ.
034400     WRITE N-RECORD INVALID KEY GO TO 5298-ERROR.
034500     IF NEW-STATUS NOT EQUAL ZERO
034600       GO TO 5298-ERROR.
034700     GO TO 5299-EXIT.
034800
034900 5298-ERROR.
035000     IF DEBUG-ON
035100       DISPLAY '5298-ERROR'.
035200     DISPLAY '---ERROR WRITING TO OUTPUT VSAM FILE-----------'.
035300     DISPLAY '*****START OF RECORD*********'.
035400     DISPLAY W-RECORD.
035500     DISPLAY 'VSAM STATUS=' NEW-STATUS.
035600     DISPLAY '*****END OF RECORD*********'.
035700     MOVE 16 TO RETURN-CODE.
035800     GO TO 6900-CLOSE-FILE.
035900
036000 5299-EXIT. EXIT.
036100
036200 6900-CLOSE-FILE.
036300     IF DEBUG-ON
036400       DISPLAY '6900-CLOSE-FILE'.
036500     CLOSE NEW-REPORT-DETAIL-FILE.
036600
036700 7000-END-LOAD. EXIT.
036800
036900 9000-CLOSE-FILES SECTION.
037000     MOVE ALL '-' TO ADD-PRINT-RECORD.
037100     WRITE ADD-PRINT-RECORD
037200              INVALID KEY GO TO 9400-PRINT-ERR.
037300     MOVE SPACES TO ADD-PRINT-RECORD.
037400     MOVE   ' TOTAL VSAM DETAIL RECORDS READ IN - ' TO PRT-ONE.
037500     MOVE   DD-VSM-COUNT TO PRT-NUMBER.
037600     WRITE ADD-PRINT-RECORD
037700              INVALID KEY GO TO 9400-PRINT-ERR.
037800     MOVE ALL '-' TO ADD-PRINT-RECORD.
037900     WRITE ADD-PRINT-RECORD
038000              INVALID KEY GO TO 9400-PRINT-ERR.
038100     MOVE SPACES TO ADD-PRINT-RECORD.
038200     MOVE ' TOTAL DETAIL RECORDS IN OUT FILE.' TO PRT-ONE.
038300     MOVE DD-TOT-COUNT TO PRT-NUMBER.
038400     WRITE ADD-PRINT-RECORD
038500              INVALID KEY GO TO 9400-PRINT-ERR.
038600     MOVE SPACES TO ADD-PRINT-RECORD.
038700     MOVE ALL '-' TO ADD-PRINT-RECORD.
038800     WRITE ADD-PRINT-RECORD
038900              INVALID KEY GO TO 9400-PRINT-ERR.
039000     MOVE SPACES TO ADD-PRINT-RECORD.
039100     MOVE  ' TOTAL DETAIL RECORDS DELETED.' TO PRT-ONE.
039200     MOVE  DD-DEL-COUNT TO PRT-NUMBER.
039300     WRITE ADD-PRINT-RECORD
039400              INVALID KEY GO TO 9400-PRINT-ERR.
039500     MOVE SPACES TO ADD-PRINT-RECORD.
039600     MOVE ALL '-' TO ADD-PRINT-RECORD.
039700     WRITE ADD-PRINT-RECORD
039800              INVALID KEY GO TO 9400-PRINT-ERR.
039900     MOVE SPACES TO ADD-PRINT-RECORD.
040000     MOVE ' TOTAL DETAIL RECORDS ADDED.' TO PRT-ONE.
040100     MOVE DD-ADD-COUNT TO PRT-NUMBER.
040200     WRITE ADD-PRINT-RECORD
040300              INVALID KEY GO TO 9400-PRINT-ERR.
040400     MOVE SPACES TO ADD-PRINT-RECORD.
040500     MOVE ALL '-' TO ADD-PRINT-RECORD.
040600     WRITE ADD-PRINT-RECORD
040700              INVALID KEY GO TO 9400-PRINT-ERR.
040800     MOVE SPACES TO ADD-PRINT-RECORD.
040900     MOVE  ' VSAM DETAIL RECORDS WRITTEN OUT.' TO PRT-ONE.
041000     MOVE DD-WRT-COUNT TO PRT-NUMBER.
041100     WRITE ADD-PRINT-RECORD
041200              INVALID KEY GO TO 9400-PRINT-ERR.
041300     MOVE ALL '-' TO ADD-PRINT-RECORD.
041400     WRITE ADD-PRINT-RECORD
041500              INVALID KEY GO TO 9400-PRINT-ERR.
041600     CLOSE REPORT-LIST-FILE.
041700     GO TO 9999-END-OF-PROGRAM.
041800
041900 9300-OUT-SPACE.
042000     DISPLAY '*** OUT OF SPACE IN PURGE ARRAY**'.
042100     MOVE 16 TO RETURN-CODE
042200     GO TO 9000-CLOSE-FILES.
042300
042400 9400-PRINT-ERR.
042500     DISPLAY '*** ERROR WRITING TO RPTLIST  '.
042600     MOVE 16 TO RETURN-CODE
042700     GO TO 9000-CLOSE-FILES.
042800
042900 9999-END-OF-PROGRAM.
043000     STOP RUN.
