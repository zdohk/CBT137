R015     TITLE 'BATCH EXTRACT OF SYSOUT FOR REPORT SYSTEM'
         PRINT NOGEN
*----------------------------------------------------------------------
* THIS PROGRAM WILL READ A SYSOUT FILE THAT HAS A PRINTOUT IN IT
* AND PRODUCE AN INDEX FILE AND A DETAIL FILE THAT CONTAINS THE SYSOUT
* DATA.  EITHER SYSOUT FORMAT MAY BE USED.  THAT IS ASA OR
* CARRIAGE CONTROL CHARATERS EITHER MAY BE USED.
*----------------------------------------------------------------------
****************************************
*     (C) COPYRIGHT BILLY FENWICK
****************************************
*  R1   AVAILABLE
*  R2   TITLE TABLE
*  R3   INPUT RECORD
*  R4   AVAILABLE
*  R5   AVAILABLE
*  R6   AVAILABLE
*  R7   AVAILABLE
*  R8   AVAILABLE
*  R9   DETAIL OUTPUT RECORD
*  R10  AVAILABLE
*  R11  AVAILABLE
*  R12  BASE
*  R13  BASE
*  R14  LINKAGE
*  R15  AVAILABLE
         SPACE
*----------------------------------------------------------------------
* LINKAGE SECTION -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
REPRT015 CSECT
         B     72(0,R15)
         DC    17F'0'
         STM   R14,R12,12(R13)
         ST    R13,4(0,R15)
         ST    R15,8(0,R13)
         LR    R13,R15
         LA    R12,4095(0,R13)
         LA    R12,1(0,R12)
         LR    R9,R12
         LA    R9,4095(0,R9)
         LA    R9,1(0,R9)
         USING REPRT015,R13
         USING REPRT015+4096,R12
         USING REPRT015+4096+4096,R9
         B     MAIN
         SPACE
*----------------------------------------------------------------------
* MAIN PROCESSING -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
MAIN     BAL   R14,BEGIN                         INITIALIZATION
         BAL   R14,GETSYSIN                      BUILD EXTRACT LIST
         BAL   R14,GETITLES                      BUILD TITLES TABLE
MAINLUP  BAL   R14,GETREC                        GET A RECORD
         BAL   R14,ANALYZE                       BUILD OUPUT RECORD
         B     MAINLUP                           LOOP REPORT FILE
RPTEOF   EQU   *
         CLI   WRTBRCH,C'Y'           DID WE UPDATE THE LAST RECORD
         BNE   CHKWRT                 BRANCH IF NOT
         PUT   RPL=GETWRK             UPDATE RECORD
         LTR   R15,R15
         BNZ   ERROR06
CHKWRT   CLI   WRTBNK,C'Y'            DID WE UPDATE THE LAST RECORD
         BNE   CHKWRT1                BRANCH IF NOT
         PUT   RPL=GETWRK2             UPDATE RECORD
         LTR   R15,R15
         BNZ   ERROR06
CHKWRT1  CLI   WRTOVR,C'Y'            DID WE UPDATE THE LAST RECORD
         BNE   CHKDDA                 BRANCH IF NOT
         PUT   RPL=GETWRK3             UPDATE RECORD
         LTR   R15,R15
         BNZ   ERROR06
CHKDDA   CLI   DDASTMT,C'Y'              ARE WE DOING DDA STATEMENTS
         BNE   RPTEOF1                   BRANCH IF NOT
         BAL   R14,WTRRECS            WRITE OUT LAST INDEX RECORDS
RPTEOF1  EQU   *
         B     EOJ                               END OF JOB
         EJECT
*----------------------------------------------------------------------
* INITIALIZATION PROCESSING -------------------------------------------
*----------------------------------------------------------------------
         SPACE
BEGIN    ST    R14,BEGIN14
         SPACE
         TIME
         SPACE
         BAL   R14,GETDATE                       CONVERT DATE ROUTINE
         SPACE
         MVC   TODAY,HLDDATE
         MVC   SCDATE(2),YEAR
         MVC   SCDATE+2(2),MTH
         MVC   SCDATE+4(2),DAY
         GETMAIN R,LV=81920
         SPACE
         LTR   R15,R15
         BZ    GOTSP1
         ST    R15,RETCD
         SPACE
         B     EARLYOUT
         USING RDSECT,R2
GOTSP1   LR    R2,R1               LOAD ADRESS OF TITLE TABLE
         MVC   0(4,R1),ZEROS                     INITIALIZE RDW
         ST    R2,SAVETIT          SAVE ADRESS OF TITLE TABLE
         A     R2,=F'40960'        GET ADDRESS OF REPORT TABLE
         ST    R2,SAVERID          SAVE ADRESS OF REPORT TABLE
         SPACE
         OPEN  (RPTIN)
         LTR   R15,R15
         BZ    NXTOPEN
         ST    R15,RETCD
         WTO   'OPEN ERROR ON RPTIN FILE'
         B     EARLYOUT
NXTOPEN  OPEN  (RPTOUT,(OUTPUT))
         LTR   R15,R15
         BZ    NXTOPEN1
         ST    R15,RETCD
         WTO   'OPEN ERROR ON RPTOUT FILE'
         B     EARLYOUT
NXTOPEN1 OPEN  (WORK1)
         LTR   R15,R15
         BZ    NXTOPEN2
         ST    R15,RETCD
         WTO   'OPEN ERROR ON WORK1 FILE'
         B     EARLYOUT
NXTOPEN2 OPEN  (WORK2)
         LTR   R15,R15
         BZ    NXTOPEN3
         ST    R15,RETCD
         WTO   'OPEN ERROR ON WORK2 FILE'
         B     EARLYOUT
NXTOPEN3 OPEN  (WORK3)
         LTR   R15,R15
         BZ    NXTOPEN4
         ST    R15,RETCD
         WTO   'OPEN ERROR ON WORK3 FILE'
         B     EARLYOUT
NXTOPEN4 OPEN  (INDEX)
         LTR   R15,R15
         BZ    NXTOPEN5
         ST    R15,RETCD
         WTO   'OPEN ERROR ON INDEX FILE'
         B     EARLYOUT
NXTOPEN5 OPEN  (IDXOUT,(OUTPUT))
         LTR   R15,R15
         BZ    GETOUT
         ST    R15,RETCD
         WTO   'OPEN ERROR ON IDXOUT FILE'
         B     EARLYOUT
GETOUT   EQU   *
         L     R14,BEGIN14
         BR    R14
         SPACE
BEGIN14  DS    F
         EJECT
*----------------------------------------------------------------------
* INPUT RECORD PROCESSING ---------------------------------------------
*----------------------------------------------------------------------
         SPACE
GETREC   ST    R14,GETREC14
GETRPT   GET   RPTIN
         SPACE
         LR    R3,R1
         ST    R3,SAVEREC      SAVE ADDRESS OF SYSOUT RECORD
         AP    TOTLINES,=P'1'  ADD 1 TO TOTAL NUMBER OF LINES
         CLI   0(R3),C'1'        NEW PAGE ASA CODE
         BE    CHECKTB           YES
         CLI   0(R3),X'89'       NEW PAGE CCW CODE
         BE    CHECKTB           YES
         CLI   0(R3),X'8B'       NEW PAGE CCW CODE
         BE    CHECKTB           YES
         B     GETEXIT
CHECKTB  L     R2,SAVETIT          LOAD ADRESS OF FIRST TITLE ENTRY
         CLI   FIRST,C'X'          IS THIS FIRST SKIP
         BE    GETEXIT             YES, GET OUT
         AP    TOTPAGES,=P'1'      ADD 1 TO TOTAL NUMBER OF PAGES
         CLI   DDASTMT,C'Y'              ARE WE DOING DDA STATEMENTS
         BE    TITHIT                    BRANCH IF YES
CHECKNX  LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         CLI   0(R2),X'FF'         END OF TABLE?
         BE    NOHIT               YES, THEN NO HIT
*  CHECK FOR FIRST PART OF TITLE DATA
         ZAP   DWD,RPTTL1          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPTRP1          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,RPTRL1            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCT1                      IS THIS OUR TITLE?
         BE    TIT1HIT                       YES,  WE HAVE A HIT
         LA    R2,REPT2(R2)                  NO, THEN TRY NEXT TITLE
         B     CHECKNX
TIT1HIT  LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
*  CHECK FOR SECOND PART OF TITLE DATA
         CP    RPTRP2,=P'0'        DO WE HAVE A SECOND LINE TO CHECK
         BE    TIT2HIT             BRANCH IF NO,  AND CHECK NEXT ONE
         ZAP   DWD,RPTTL2          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPTRP2          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,RPTRL2            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCT2                      IS THIS OUR TITLE?
         BE    TIT2HIT                       YES,  WE HAVE A HIT
         LA    R2,REPT2(R2)                  NO, THEN TRY NEXT TITLE
         B     CHECKNX
TIT2HIT  LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
*  CHECK FOR THIRD PART OF TITLE DATA
         CP    RPTRP3,=P'0'        DO WE HAVE A SECOND LINE TO CHECK
         BE    TITHIT              BRANCH IF NO,  AND CHECK NEXT ONE
         ZAP   DWD,RPTTL3          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPTRP3          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,RPTRL3            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCT3                      IS THIS OUR TITLE?
         BE    TITHIT                        YES,  WE HAVE A HIT
         LA    R2,REPT2(R2)                  NO, THEN TRY NEXT TITLE
         B     CHECKNX
TITHIT   CLI   WRTPAGE,C' '      DID PAGE HAVE ANYTHING ON IT
         BE    NOHIT             BRANCH IF NOT
         ST    R2,R2SAVE         SAVE TITLE RECORD ADDRESS
         BAL   R14,GETIDTE           GET THE REPORT DATE
         CLI   BADDATE,C'X'      IS THIS A BAD DATE RECORD
         BE    NOHIT             YES, THEN NO APPLICATION INDEX RECORD
         BAL   R14,BLDBNK            GET THE BANK NUMBER
         BAL   R14,BLDBRCH           GET THE BRANCH NUMBER
         MVI   PGNOT,C'R'            MAIN REPORT ID RECORD
         BAL   R14,CHECKOLD          CHECK CURRENT INDEX FILE
         CLI   IDXHIT,C'Y'           IS THEIR ALREADY AN INDEX RECORD
         BE    NOHIT                 YES,  THEN DO NOT EXTRACT
         BAL   R14,CHECKBNK          CHECK FOR EXTRACT BY BANK
         CLI   BNKHIT,C'Y'           DO WE EXTRACT THIS ONE
         BE    DOREPT                YES
         CLI   SBNKHIT,C'Y'          DO WE EXTRACT THIS ONE
         BE    DOREPT                YES
         B     NOHIT                 DO NOT EXTRACT THIS ONE
DOREPT   BAL   R14,GETREPT           ADD/UPDATE/GET REPT RECORD
         CLI   BNKHIT,C'Y'           MAIN ID HIT
         BNE   DOREPT1               NO,  THEN DO ADD TO MAIN ID RECS
         BAL   R14,ADDBRCH           ADD TO THE BRANCH WORK FILE
         BAL   R14,ADDBANK           ADD TO THE BANK WORK FILE
DOREPT1  BAL   R14,PUTDETAL          WRITE OUT DETAL RECORD
         BAL   R14,BLDKREC           BUILD THE 'K' RECORD
         BAL   R14,BLDIREC           BUILD THE 'I' RECORD
         CLI   DDASTMT,C'Y'              ARE WE DOING DDA STATEMENTS
         BNE   NOHIT                     BRANCH IF NOT
         BAL   R14,BLDSREC           BUILD THE 'S' RECORD
NOHIT    BAL   R14,CLEARDTL          CLEAR THE DETAIL RECORD
         SPACE
GETEXIT  L     R14,GETREC14
         BR    R14
         SPACE
GETREC14 DS    F
CLCT1    CLC   0(0,R8),RPTTITD1
CLCT2    CLC   0(0,R8),RPTTITD2
CLCT3    CLC   0(0,R8),RPTTITD3
         EJECT
*----------------------------------------------------------------------
* INPUT SYSIN RECORDS FOR PROCESSING  ---------------------------------
*----------------------------------------------------------------------
         SPACE
GETSYSIN ST    R14,GETREC14
         OPEN  (CNTLFILE)          OPEN SYSIN FILE
         LTR   R15,R15
         BZ    GETSIN
         WTO   'OPEN ERROR ON SYSIN FILE'
         ST    R15,RETCD
         B     EARLYOUT
GETSIN   GET   CNTLFILE             GET SYSIN CONTROL CARD
         SPACE
         CLC   0(5,R1),=C'DATE='    DO WE HAVE A RUN DATE
         BE    GETRDATE             YES,  THEN SAVE IT
DOSYSIN  MVC   SYSINREC,0(R1)       MOVE NEW SYSIN RECORD INTO TABLE
         CLC   SYSINREC(8),=C'DDASTMTS'  ARE WEE DOING DDA STATEMENTS
         BNE   CLOSEIN                   BRANCH IF NOT
         MVI   DDASTMT,C'Y'         GET DDA STATEMENT PROCESSING FLAG
CLOSEIN  EQU   *
         CLOSE (CNTLFILE)           CLOSE SYSIN FILE
         L     R14,GETREC14
         BR    R14
GETRDATE MVC   RCDATE(2),11(R1)         MOVE YY
         MVC   RCDATE+2(2),5(R1)        MOVE MM
         MVC   RCDATE+4(2),8(R1)        MOVE DD
         GET   CNTLFILE             GET SYSIN CONTROL CARD
         B     DOSYSIN
CNTLEOF  EQU   *
         B     ERROR15
         EJECT
*----------------------------------------------------------------------
* INPUT TITLE RECORDS FOR PROCESSING  ---------------------------------
*----------------------------------------------------------------------
         SPACE
*      NUMBER OF TITLE AND REPORT ID RECORDS THE BUFFERS WILL HOLD
NMTITREC EQU   40960/REPT2         TITLE ID RECORDS
NMRPTREC EQU   40960/REPT3         REPORT ID RECORDS
         USING RDSECT,R4
         USING RTDSECT,R5
GETITLES ST    R14,GETREC14
         CLI   DDASTMT,C'Y'              ARE WE DOING DDA STATEMENTS
         BE    GETDDAST                  BRANCH IF YES
         OPEN  (TITLES)
         LTR   R15,R15
         BZ    TITLEOK
         WTO   'OPEN ERROR ON TITLES FILE'
         ST    R15,RETCD
         B     EARLYOUT
TITLEOK  L     R2,SAVETIT     LOAD STARTING ADDRESS OF TITLE TABLE
         L     R3,SAVERID     LOAD STARTING ADDRESS OF REPORT TABLE
         SR    R6,R6
         SR    R7,R7
         LA    R8,NMTITREC           LOAD MAX # OF TITLE RECORDS
         LA    R10,NMRPTREC          LOAD MAX # REPORT RECORDS
GETTIT   GET   RPL=GETRPL
         SPACE
         LTR   R15,R15
         BNZ   ERROR02
         L     R4,RECADDR       LOAD ADDRESS OF RECORD
         L     R5,RECADDR       LOAD ADDRESS OF RECORD
         CLI   RPTYPE,C'T'      IS THIS A TITLE RECORD
         BE    MVTIT            YES
         CLI   TPTYPE,C'R'      IS THIS A REPORT RECORD
         BE    MVRPT            YES
         B     GETTIT           GET NEXT RECORD
MVTIT    LA    R11,APPLID       START OF APPLICATION LIST
         CLI   SYSINREC,C'T'   ARE WE USING TITLE IDS
         BE    CHKTITL         YES, THEN BRANCH
CHKTAPL  CLC   0(L'RPTAPL,R11),BLANKS    END OF APPLICATIONS LIST
         BE    GETTIT           YES, THEN FORGET IT
         CLC   RPTAPL,0(R11)    DOES THE APPLICATION MATCH
         BE    GOTTAPL          YES THEN MOVE IT
         LA    R11,4(R11)       BUMP TO NEXT APLLICATION ID
         B     CHKTAPL          TRY AGAIN
CHKTITL  CLC   0(L'RTID,R11),BLANKS    END OF TITLES LIST
         BE    GETTIT           YES, THEN FORGET IT
         CLC   RTID,0(R11)      DOES THE TITLE ID MATCH
         BE    GOTTAPL          YES THEN MOVE IT
         LA    R11,9(R11)       BUMP TO NEXT TITLE ID
         B     CHKTITL          TRY AGAIN
GOTTAPL  EQU   *
         CLI   RPTEXT,C'Y'      DO WE EXTRACT THIS TITLE
         BE    GOTTID           YES, THEN PUT IT TABLE
         CLI   RPTEXT,C'P'      DO WE EXTRACT THIS TITLE
         BE    GOTTID           YES, THEN PUT IT TABLE
         B     GETTIT           NO, THEN DON'T PUT IT IN TABLE
GOTTID   MVC   0(255,R2),0(R4)   MOVE NEW TITLE RECORD INTO TABLE
         MVC   255(REPT2-255,R2),255(R4)
         LA    R6,1(R6)         COUNT NUMBER OF RECORDS STORED
         CR    R6,R8            IS TABLE FULL
         BE    ERROR03          YES, THEN QUIT
         LA    R2,REPT2(R2)       BUMP TO NEXT SLOT
         B     GETTIT           GET NEXT RECORD
MVRPT    LA    R11,APPLID       START OF APPLICATION LIST
         CLI   SYSINREC,C'T'   ARE WE USING TITLE IDS
         BE    CHKRITL         YES, THEN BRANCH
CHKRAPL  CLC   0(L'TPTAPL,R11),BLANKS    END OF APPLICATIONS LIST
         BE    GETTIT           YES, THEN FORGET IT
         CLC   TPTAPL,0(R11)    DOES THE APPLICATION MATCH
         BE    GOTRAPL          YES THEN MOVE IT
         LA    R11,4(R11)       BUMP TO NEXT APLLICATION ID
         B     CHKRAPL          TRY AGAIN
CHKRITL  CLC   0(L'TTID,R11),BLANKS    END OF TITLES LIST
         BE    GETTIT           YES, THEN FORGET IT
         CLC   TTID,0(R11)      DOES THE TITLE ID MATCH
         BE    GOTRAPL          YES THEN MOVE IT
         LA    R11,9(R11)       BUMP TO NEXT TITLE ID
         B     CHKRITL          TRY AGAIN
GOTRAPL  EQU   *
         CLI   TPTEXT,C'Y'      DO WE EXTRACT THIS SECONDARY TITLE
         BE    GOTTIR           YES, THEN PUT IT IN TABLE
         CLI   TPTEXT,C'P'      DO WE EXTRACT THIS SECONDARY TITLE
         BE    GOTTIR           YES, THEN PUT IT IN TABLE
         B     GETTIT           NO, THEN DON'T PUT IT IN TABLE
GOTTIR   MVC   0(REPT3,R3),0(R5)  MOVE NEW REPORT RECORD INTO TABLE
         LA    R7,1(R7)         COUNT NUMBER OF RECORDS STORED
         CR    R7,R10           IS TABLE FULL
         BE    ERROR04          YES, THEN QUIT
         LA    R3,REPT3(R3)       BUMP TO NEXT SLOT
         B     GETTIT           GET NEXT RECORD
GETDDAST L     R2,SAVETIT     LOAD STARTING ADDRESS OF TITLE TABLE
         L     R3,SAVERID     LOAD STARTING ADDRESS OF REPORT TABLE
         MVC   0(255,R2),DDATITLE    MOVE DDA REPORT RECORD
         MVC   255(REPT2-255,R2),DDATITLE+255
         LA    R2,REPT2(R2)       BUMP TO NEXT SLOT
         MVI   0(R2),X'FF'      SET END OF TABLE
         MVI   0(R3),X'FF'      SET END OF TABLE
         B     NOMTITL
TITEOF   MVI   0(R2),X'FF'      SET END OF TABLE
         MVI   0(R3),X'FF'      SET END OF TABLE
         CLOSE TITLES
         LTR   R15,R15
         BZ    NOMTITL
         B     ERROR01
NOMTITL  L     R14,GETREC14
         BR    R14
         DROP  R4
         DROP  R5
         EJECT
*----------------------------------------------------------------------
* FIND REPORT DATE  ---------------------------------------------------
*    VALID DATE FORMAT TYPE ARE LISTED BELOW.
*        'N'           NORMAL DATE FORMAT MM-DD-YY
*        'C'           DATE FORMAT MM-DD-YYYY
*        'Y'           DATE FORMAT YY-MM-DD
*        'R'           USE THE RUN DATE FROM THE CONTROL CARD
*        'S'           USE THE SYSTEM DATE
*----------------------------------------------------------------------
         SPACE
GETIDTE  ST    R14,GETDTE14
         SPACE
         MVI   BADDATE,C' '        CLEAR BAD DATE INDICATOR
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,RPDTL           SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPDTP           SETUP TO ADD
         CVB   R4,DWD                DATE POSITION
         BCTR  R4,0                 SUBTRTACT ONE FOR COMPARE
         AR    R8,R4                POINT TO WHERE DATE SHOULD BE
         CLI   RPDTT,C'N'           NORMAL DATE FORMAT MM-DD-YY
         BE    DATENORM             YES
         CLI   RPDTT,C'C'           DATE FORMAT MM-DD-YYYY
         BE    DATECENY             YES
         CLI   RPDTT,C'Y'           DATE FORMAT YY-MM-DD
         BE    DATEBACK             YES
         CLI   RPDTT,C'R'           USE RUN DATE FROM CONTROL CARD
         BE    DATERUN              YES
         CLI   RPDTT,C'S'           USE SYSTEM DATE
         BE    DATESYS              YES
         MVC   XDATE(6),=C'000000'
         B     DTEEXIT
DATERUN  MVC   XDATE(6),RCDATE           MOVE IN RUN DATE
         B     SETDATE
DATESYS  MVC   XDATE(6),SCDATE           MOVE IN SYSTEM DATE
         B     SETDATE
DATEBACK MVC   XDATE(2),0(R8)            GET YEAR
         MVC   XDATE+2(2),3(R8)          GET MONTH
         MVC   XDATE+4(2),6(R8)          GET DAY
         B     SETDATE
DATECENY MVC   XDATE(2),8(R8)            GET YEAR
         MVC   XDATE+2(2),0(R8)          GET MONTH
         MVC   XDATE+4(2),3(R8)          GET DAY
         B     SETDATE
DATENORM MVC   XDATE(2),6(R8)            GET YEAR
         MVC   XDATE+2(2),0(R8)          GET MONTH
         MVC   XDATE+4(2),3(R8)          GET DAY
SETDATE  LA    R4,6
         LA    R5,XDATE
CKDATE   CLI   0(R5),C'0'         IS DATE NUMERIC
         BL    NODATE             YES, THEN GET OUT
         CLI   0(R5),C'9'         IS DATE NUMERIC
         BH    NODATE             YES, THEN GET OUT
CKOK     LA    R5,1(R5)           BUMP TO NEXT DIGIT
         BCT   R4,CKDATE
         B     DTEEXIT            DATE IS OK, THEN GET OUT
NODATE   EQU   *
         CLI   0(R5),C' '         IS THIS POSITION A BLANK
         BNE   NOBLANK            BRANCH IF NOT BLANK
         MVI   0(R5),C'0'         OK THEN MAKE IT A ZERO
         B     CKOK               NOW GET NEXT DIGIT
NOBLANK  MVC   XDATE,OLDDATE      RESTORE OLD DATE
         MVI   BADDATE,C'X'        SET BAD DATE INDICATOR
         SPACE
DTEEXIT  EQU   *
         CLC   XDATE,=C'000000'   IS OUR ZERO
         BNE   DTEOUT
         MVI   BADDATE,C'X'        SET BAD DATE INDICATOR
         MVC   XDATE,OLDDATE      RESTORE OLD DATE
DTEOUT   MVC   OLDDATE,XDATE      SAVE PREVIOUS DATE
         L     R14,GETDTE14
         BR    R14
         SPACE
GETDTE14 DS    F
         EJECT
*----------------------------------------------------------------------
* DIFFERENT COMMON ROUTINES   -----------------------------------------
*----------------------------------------------------------------------
         SPACE
*   ROUTINE TO CONVERT A PACKED NUMBER TO HEX AND ADD THE RESULT TO
*   A REGISTER
CNVHEX   CVB   R6,DWD
         BCTR  R6,0
         L     R5,=F'133'
         SR    R4,R4
         MR    R4,R6
         AR    R8,R5
         BR    R14
         EJECT
*----------------------------------------------------------------------
* WRITE DETAIL RECORD OUT ---------------------------------------------
*----------------------------------------------------------------------
         SPACE
PUTDETAL ST    R14,PUTDET14
         SPACE
         L     R2,R2SAVE
         CLI   BNKHIT,C'Y'         DO WE EXTRACT THIS ONE
         BE    OKDETAIL            YES
         CLI   SBNKHIT,C'Y'        DO WE EXTRACT THIS ONE
         BE    OKDETAIL            YES
         B     DETALEXT            THEN GET OUT
OKDETAIL MVC   DRECID,RTID         MOVE TITLE ID
         ZAP   DPAGE,APPAGES       MOVE PAGE NUMBER
         MVC   DDATE,XDATE         MOVE REPORT DATE
         ZAP   DNLINES,LINES       NUMBER OF LINES ON THIS PAGE
         AP    TOTPGS,=P'1'        NUMBER OF PAGES OUTPUT
         AP    TOTLNS,DNLINES      TOTAL NUMBER OF LINES OUTPUT
         ZAP   DWD,DNLINES         COMPUTE THE
         CVB   R6,DWD               RECORD LENGTH
         L     R5,=F'133'           OF THE
         SR    R4,R4                DETAIL RECORD
         MR    R4,R6               NOW WE HAVE THE VARIABLE PART
         AH    R5,DLENGTH           LET'S THE FIX PART
         STH   R5,DRECLNG          STORE RECORD LENGTH
* PUT RECORD LENGTH HERE
         PUT   RPTOUT,DRECORD      WRITE OUT DETAIL RECORD
         SPACE
DETALEXT L     R14,PUTDET14
         BR    R14
         SPACE
PUTDET14 DS    F
         EJECT
*----------------------------------------------------------------------
* FIND THE BANK NUMBER FOR THIS PAGE   -------------------------------
*----------------------------------------------------------------------
         SPACE
BLDBNK   ST    R14,BLDKRC14
         SPACE
         CP    RPTFRC,=P'0'        DO WE FORCE A BANK NUMBER
         BE    BNKOK               BRANCH ZERO,  NO FORCE BANK #
         ZAP   BANKNUM,RPTFRC      FORCE BANK NUMBER
         B     BNKRTN              GET OUT
BNKOK    LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         CP    RPBRL,=P'0'         IS BANK LENGTH ZERO?
         BE    BNKERR                YES
         ZAP   DWD,RPBTL           NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPBRP           SETUP TO ADD
         CVB   R4,DWD                BANK POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE BANK SHOULD BE
         ZAP   DWD,RPBRL             CONVERT BANK NUMBER
         CVB   R4,DWD                    LENGTH TO HEX
         LR    R6,R8
         LR    R5,R4
         BCTR  R4,0                      FOR EX OF PACK INSTRUCTION
BNK1     CLI   0(R6),C'0'        IS DIGIT NUMERIC
         BL    BNK2              NO,  CHECK FOR BLANK
         CLI   0(R6),C'9'        IS DIGIT NUMBERIC
         BH    BNKERR            NO, BAD DIGIT GET OUT
BNKTRY   LA    R6,1(R6)          BUMP TO NEXT DIGIT
         BCT   R5,BNK1           TRY AGAIN
         EX    R4,BNKPACK                FOR THE BANK NUMBER
         SPACE
BNKRTN   L     R14,BLDKRC14
         BR    R14
BNKPACK  PACK  BANKNUM,0(0,R8)
BNKERR   ZAP   BANKNUM,=P'0'   CLEAR OUT BANK NUMBER
         B     BNKRTN
BNK2     CH    R5,=H'1'      IS THIS THE LAST DIGIT
         BE    BNKERR        YES THEN NOT OK,  GET OUT
         CLI   0(R6),C' '    IS THIS DIGIT BLANK
         BE    BNKTRY
         B     BNKERR
         EJECT
*----------------------------------------------------------------------
* FIND THE BRANCH NUMBER FOR THIS PAGE   ------------------------------
*----------------------------------------------------------------------
         SPACE
BLDBRCH  ST    R14,BLDKRC14
         SPACE
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         CP    RPRRL,=P'0'         IS BANK LENGTH ZERO?
         BE    BRCHERR               YES
         ZAP   DWD,RPRTL           NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPRRP           SETUP TO ADD
         CVB   R4,DWD                BRANCH POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE BRANCH SHOULD BE
         ZAP   DWD,RPRRL             CONVERT BRANCH NUMBER
         CVB   R4,DWD                    LENGTH TO HEX
         LR    R6,R8
         LR    R5,R4
         BCTR  R4,0                      FOR EX OF PACK INSTRUCTION
BRCH1    CLI   0(R6),C'0'        IS DIGIT NUMERIC
         BL    BRCH2             NO,  CHECK FOR BLANK
         CLI   0(R6),C'9'        IS DIGIT NUMBERIC
         BH    BRCHERR           NO, BAD DIGIT GET OUT
BRCHTRY  LA    R6,1(R6)          BUMP TO NEXT DIGIT
         BCT   R5,BRCH1          TRY AGAIN
         EX    R4,BRCHPACK               FOR THE BRANCH NUMBER
         SPACE
BRCHRTN  L     R14,BLDKRC14
         BR    R14
BRCHPACK PACK  BRCHNUM,0(0,R8)
BRCHERR  ZAP   BRCHNUM,=P'0'   CLEAR OUT BRANCH NUMBER
         B     BRCHRTN
BRCH2    CH    R5,=H'1'      IS THIS THE LAST DIGIT
         BE    BRCHERR       YES THEN NOT OK,  GET OUT
         CLI   0(R6),C' '    IS THIS DIGIT BLANK
         BE    BRCHTRY
         B     BRCHERR
         EJECT
*----------------------------------------------------------------------
* CLEAR THE DETAIL RECORD ---------------------------------------------
*----------------------------------------------------------------------
         SPACE
CLEARDTL ST    R14,CLEAR14
         SPACE
         MVC   DRECID(30),BLANKS
         LA    R5,DLINE
         L     R4,=F'87'           MAX NUMBER OF LINES ON A PAGE
CLEARLP  MVC   0(L'DLINE,R5),BLANKS  BLANK ONE LINE
         LA    R5,133(R5)          DUMP TO NEXT LINE
         BCT   R4,CLEARLP
         ZAP   LINES,=P'0'         CLEAR LINES ON THIS PAGE
         MVI   WRTPAGE,C' '
         SPACE
         L     R14,CLEAR14
         BR    R14
         SPACE
CLEAR14 DS    F
         EJECT
*----------------------------------------------------------------------
* BUILD THE KEY/INDEX REPORT RECORD TYPE 'K' --------------------------
*----------------------------------------------------------------------
         SPACE
BLDKREC  ST    R14,BLDKRC14
         SPACE
         L     R2,R2SAVE
         CLI   BNKHIT,C'Y'         DO WE EXTRACT THIS ONE
         BNE   BLDK5               BRANCH IF NOT TO EXTRACT
         MVC   KTITID,RTID         MOVE TITLE ID
         MVC   KRIDID,RRID         MOVE REPORT ID
         ZAP   KPAGE,APPAGES       MOVE PAGE NUMBER
         MVC   KDATE,XDATE         MOVE REPORT DATE
         MVI   KTYPE,C'K'          MOVE RECORD TYPE
         ZAP   KBANK,BANKNUM       MOVE BANK NUMBER
         MVC   KKEYV,BLANKS        CLEAR KEY VALUE
         LA    R7,KKEYV               SET ADDRES OF KEY VALUE FIELD
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         CP    RPKRL1,=P'0'        IS KEY 1 LENGTH ZERO?
         BE    BLDK2               YES
         ZAP   DWD,RPKTL1          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPKRP1          SETUP TO ADD
         CVB   R4,DWD                KEY 1 POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE KEY 1 SHOULD BE
         ZAP   DWD,RPKRL1            CONVERT KEY 1 LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR MOVE
         EX    R4,BLDMVC5                    OF THE KEY 1 VALUE
         AR    R7,R4                     BUMP TO NEXT AVAILABLE BYTE
         LA    R7,1(R7)                  BUMP BY ONE
BLDK2    CP    RPKRL2,=P'0'        IS KEY 2 LENGTH ZERO?
         BE    BLDK3               YES
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,RPKTL2          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPKRP2          SETUP TO ADD
         CVB   R4,DWD                KEY 2 POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE KEY 2 SHOULD BE
         ZAP   DWD,RPKRL2            CONVERT KEY 2 LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR MOVE
         EX    R4,BLDMVC5                    OF THE KEY 2 VALUE
         AR    R7,R4                     BUMP TO NEXT AVAILABLE BYTE
         LA    R7,1(R7)                  BUMP BY ONE
BLDK3    CP    RPKRL3,=P'0'        IS KEY 3 LENGTH ZERO?
         BE    BLDK4               YES
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,RPKTL3          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPKRP3          SETUP TO ADD
         CVB   R4,DWD                KEY 3 POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE KEY 3 SHOULD BE
         ZAP   DWD,RPKRL3            CONVERT KEY 3 LENGTH
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR MOVE
         EX    R4,BLDMVC5                    OF THE KEY 3 VALUE
BLDK4    EQU   *
         ZAP   KIDXL,RPKRL1          ADD THE LENGTH OF 1ST KEY
         AP    KIDXL,RPKRL2          ADD THE LENGTH OF 2ND KEY
         AP    KIDXL,RPKRL3          ADD THE LENGTH OF 3RD KEY
         CP    KIDXL,=P'0'         IS THEIR A KEY TO WRITE OUT
         BE    BLDK5               NO
         MVC   KRECLNG,KLENGTH       MOVE IN RECORD LENGTH
         ZAP   KSEQ,BPPAGES        MOVE BANK PAGE NUMBER
         ZAP   KBRANCH,=P'0'       THEN MAKE A ZERO BRANCH RECORD
         PUT   IDXOUT,KRECORD      WRITE OUT K INDEX RECORD
         SPACE
         CP    BRCHNUM,=P'0'       IS BRANCH NUMBER ZERO
         BE    BLDK5               BRANCH IF YES
         ZAP   KSEQ,PPPAGES        MOVE BRANCH PAGE NUMBER
         ZAP   KBRANCH,BRCHNUM     MOVE BRANCH NUMBER
         PUT   IDXOUT,KRECORD      WRITE OUT K INDEX RECORD
         SPACE
BLDK5    L     R14,BLDKRC14
         BR    R14
         SPACE
BLDMVC5  MVC   0(0,R7),0(R8)
BLDKRC14 DS    F
         EJECT
*----------------------------------------------------------------------
* BUILD THE KEY/INDEX REPORT RECORD TYPE 'I' --------------------------
*----------------------------------------------------------------------
         SPACE
BLDIREC  ST    R14,BLDIRC14
         SPACE
         L     R2,R2SAVE
         L     R3,SAVERID       LOAD ADDRESS OF REPORT TABLE
         CLI   SBNKHIT,C'Y'        DO WE EXTRACT THIS ONE
         BNE   NORTBL              BRANCH IF NOT TO EXTRACT
         USING RTDSECT,R3
CHKRTBL  CLC   TTID,RTID           DO TITLE ID'S MATCH
         BE    RTBLHIT             YES
TRYAGN   CLI   0(R3),X'FF'         IS THIS END OF TABLE
         BE    NORTBL              YES, THEN GET OUT
         LA    R3,REPT3(R3)        BUMP TO NEXT RECORD
         B     CHKRTBL             GET NEXT TABLE
RTBLHIT  LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,TPTTL1          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPTRP1          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,TPTRL1            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCRT1                     IS THIS OUR TITLE?
         BE    RIDHIT1                       YES,  WE HAVE A HIT
         B     TRYAGN              NO THEN TRY AGAIN
RIDHIT1  CP    TPTRL2,=P'0'        DO WE HAV A LENGTH
         BE    RIDHIT2                       YES,  WE HAVE A HIT
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,TPTTL2          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPTRP2          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,TPTRL2            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCRT2                     IS THIS OUR TITLE?
         BE    RIDHIT2                       YES,  WE HAVE A HIT
         B     TRYAGN              NO THEN TRY AGAIN
RIDHIT2  CP    TPTRL3,=P'0'        DO WE HAV A LENGTH
         BE    RIDHIT                        YES,  WE HAVE A HIT
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,TPTTL3          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPTRP3          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,TPTRL3            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCRT3                     IS THIS OUR TITLE?
         BE    RIDHIT                        YES,  WE HAVE A HIT
         B     TRYAGN              NO THEN TRY AGAIN
RIDHIT   MVC   ITITID,TTID         MOVE TITLE ID
         MVC   IRIDID,TRID         MOVE REPORT ID
         ZAP   IPAGE,APPAGES       MOVE DETAIL PAGE NUMBER
         MVC   IDATE,XDATE         MOVE REPORT DATE
         MVI   ITYPE,C'K'          MOVE RECORD TYPE
         MVC   IKEYV,BLANKS        CLEAR KEY VALUE
         ZAP   IBANK,BANKNUM       MOVE BANK NUMBER
         LA    R7,IKEYV               SET ADDRES OF KEY VALUE FIELD
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         CP    TPKRL1,=P'0'        IS KEY 1 LENGTH ZERO?
         BE    BLDT2               YES
         ZAP   DWD,TPKTL1          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPKRP1          SETUP TO ADD
         CVB   R4,DWD                KEY 1 POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE KEY 1 SHOULD BE
         ZAP   DWD,TPKRL1            CONVERT KEY 1 LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR MOVE
         EX    R4,BLDMVC5                    OF THE KEY 1 VALUE
         AR    R7,R4                     BUMP TO NEXT AVAILABLE BYTE
         LA    R7,1(R7)                  BUMP BY ONE
BLDT2    CP    TPKRL2,=P'0'        IS KEY 2 LENGTH ZERO?
         BE    BLDT3               YES
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,TPKTL2          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPKRP2          SETUP TO ADD
         CVB   R4,DWD                KEY 2 POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE KEY 2 SHOULD BE
         ZAP   DWD,TPKRL2            CONVERT KEY 2 LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR MOVE
         EX    R4,BLDMVC5                    OF THE KEY 2 VALUE
         AR    R7,R4                     BUMP TO NEXT AVAILABLE BYTE
         LA    R7,1(R7)                  BUMP BY ONE
BLDT3    CP    TPKRL3,=P'0'        IS KEY 3 LENGTH ZERO?
         BE    BLDT4               YES
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,TPKTL3          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPKRP3          SETUP TO ADD
         CVB   R4,DWD                KEY 3 POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE KEY 3 SHOULD BE
         ZAP   DWD,TPKRL3            CONVERT KEY 3 LENGTH
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR MOVE
         EX    R4,BLDMVC5                    OF THE KEY 3 VALUE
BLDT4    EQU   *
         ZAP   IIDXL,TPKRL1          ADD THE LENGTH OF 1ST KEY
         AP    IIDXL,TPKRL2          ADD THE LENGTH OF 2ND KEY
         AP    IIDXL,TPKRL3          ADD THE LENGTH OF 3RD KEY
         MVC   IRECLNG,ILENGTH       MOVE IN RECORD LENGTH
         SPACE
         MVI   PGNOT,C'S'
         BAL   R14,ADDBRCH     ADD SECONDRARY ID TO BRANCH WORK1 FILE
         BAL   R14,ADDBANK     ADD SECONDRARY ID TO  BANK WORK2 FILE
         BAL   R14,GETREPT     ADD SECONDRARY ID TO OVERALL WORK3 FILE
         ZAP   ISEQ,BPPAGES        MOVE THE SID PAGE NUMBER
         ZAP   IBRANCH,=P'0'       MAKE ZERO BRANCH
         PUT   IDXOUT,IRECORD      WRITE OUT I INDEX RECORD
         SPACE
         CP    BRCHNUM,=P'0'       IS BRANCH NUMBER ZERO
         BE    BLDT5               BRANCH IF YES
         ZAP   IBRANCH,BRCHNUM     THEN MAKE A ZERO BRANCH RECORD
         ZAP   ISEQ,PPPAGES        MOVE THE SID PAGE NUMBER
         PUT   IDXOUT,IRECORD      WRITE OUT K INDEX RECORD
         SPACE
BLDT5    LA    R3,REPT3(R3)        BUMP TO NEXT RECORD
         CLI   0(R3),X'FF'         IS THIS END OF TABLE
         BE    NORTBL              YES, THEN GET OUT
         B     CHKRTBL             GET NEXT TABLE
         SPACE
NORTBL   L     R14,BLDIRC14
         BR    R14
CLCRT1   CLC   0(0,R8),TPTTITD1
CLCRT2   CLC   0(0,R8),TPTTITD2
CLCRT3   CLC   0(0,R8),TPTTITD3
         SPACE
BLDIRC14 DS    F
         EJECT
*----------------------------------------------------------------------
* BUILD THE APPLICATION INDEX RECORD TYPE 'S'   ---------------------
*----------------------------------------------------------------------
         SPACE
BLDSREC  ST    R14,BLDSRC14
         SPACE
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,RPKBL1          SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPKBP1          SETUP TO ADD
         CVB   R4,DWD                BANK POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE INDEX WOULD BE
         ZAP   DWD,RPKBK1            CONVERT BANK LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR MOVE
         EX    R4,BLSCLC1            IS BANK NUMBER THERE
         BL    BLDSNX                BRANCH IF LOW
         EX    R4,BLSCLC2            IS BANK NUMBER THERE
         BH    BLDSNX                BRANCH IF HIGH
         EX    R4,BLSMVC1                    MOVE THE BANK NUMBER
         NI    SCBANK+L'SCBANK-1,X'FC'
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,RPKAL1          SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPKAP1          SETUP TO ADD
         CVB   R4,DWD                ACCT POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE INDEX WOULD BE
         ZAP   DWD,RPKAK1            CONVERT ACCT LINE
         CVB   R4,DWD                        LENGTH TO HEX
         LR    R6,R8
         LR    R5,R4
         SR    R7,R7
         LA    R1,WORKARE
BLDS2T   CLI   0(R6),C'0'        CHECK FOR NUMERIC
         BL    BLDSA1            BRANCH IF LOW
         CLI   0(R6),C'9'        CHECK FOR NUMERIC
         BH    BLDSA1            BRANCH IF HIGH
         MVC   0(1,R1),0(R6)
         LA    R7,1(R7)
         LA    R1,1(R1)
BLDSA1   LA    R6,1(R6)
         BCT   R5,BLDS2T
         C     R7,=F'0'             DID WE HAVE ANY DIGITS
         BE    BLDSNX               NO, BRANCH
         BCTR  R7,0
         EX    R7,BLSMVC2                    MOVE THE ACCT NUMBER
         NI    SCACCT+L'SCACCT-1,X'FC'
         CP    SVBANKL,SCBANK      IS BANK NUMBER THE SAME
         BNE   BLDSB1              YES
         CP    SVACCTL,SCACCT      IS ACCOUNT NUMBER THE SAME
         BNE   BLDSB1              YES
         AP    SVCOUNTL,=P'1'      NUMBER OF STATEMENTS
         B     BLDSNX
BLDSB1   EQU   *
         CP    SVCOUNTL,=P'0'      IS THIS FIRST TIME THRU
         BE    BLDSF1              YES
         MVC   SRECLNG,SLENGTH     MOVE IN RECORD LENGTH
         ZAP   SPAGE,SVPAGESL      MOVE PAGE NUMBER
         MVC   SDATE,SVDATEL       MOVE REPORT DATE
         MVI   STYPE,C'S'          MOVE RECORD TYPE
         MVI   SSIDE,C'L'          LEFT SIDE
         MVC   SFILLER,BLANKS      CLEAR REMAINDER OF KEY
         MVC   SFIRST,SVFIRSTL
         ZAP   SBANK,SVBANKL
         ZAP   SACCOUNT,SVACCTL
         ZAP   SCOUNT,SVCOUNTL
         PUT   IDXOUT,SRECORD
BLDSF1   ZAP   SVBANKL,SCBANK
         ZAP   SVACCTL,SCACCT
         ZAP   SVCOUNTL,=P'1'
         MVC   SVPAGESL,APPAGES
         MVC   SVDATEL,XDATE
         MVI   SVFIRSTL,C'Y'
         CLC   DLINE+133+58(4),=C'COPY'
         BE    BLDSF11
         CLC   DLINE+133+133+58(4),=C'PAGE'
         BNE   BLDSNX
BLDSF11  MVI   SVFIRSTL,C'N'
BLDSNX   EQU   *
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,RPKBL2          SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPKBP2          SETUP TO ADD
         CVB   R4,DWD                BANK POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE INDEX WOULD BE
         ZAP   DWD,RPKBK2            CONVERT BANK LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR MOVE
         EX    R4,BLSCLC1            IS BANK NUMBER THERE
         BL    BLDS1                 BRANCH IF LOW
         EX    R4,BLSCLC2            IS BANK NUMBER THERE
         BH    BLDS1                 BRANCH IF HIGH
         EX    R4,BLSMVC1                    MOVE THE BANK NUMBER
         NI    SCBANK+L'SCBANK-1,X'FC'
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,RPKAL2          SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,RPKAP2          SETUP TO ADD
         CVB   R4,DWD                ACCT POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE INDEX WOULD BE
         ZAP   DWD,RPKAK2            CONVERT ACCT LINE
         CVB   R4,DWD                        LENGTH TO HEX
         LR    R6,R8
         LR    R5,R4
         SR    R7,R7
         LA    R1,WORKARE
BLDS3T   CLI   0(R6),C'0'        CHECK FOR NUMERIC
         BL    BLDSA2            BRANCH IF LOW
         CLI   0(R6),C'9'        CHECK FOR NUMERIC
         BH    BLDSA2            BRANCH IF HIGH
         MVC   0(1,R1),0(R6)
         LA    R7,1(R7)
         LA    R1,1(R1)
BLDSA2   LA    R6,1(R6)
         BCT   R5,BLDS3T
         C     R7,=F'0'             DID WE HAVE ANY DIGITS
         BE    BLDS1                NO, BRANCH
         BCTR  R7,0
         EX    R7,BLSMVC2                    MOVE THE ACCT NUMBER
         NI    SCACCT+L'SCACCT-1,X'FC'
         CP    SVBANKR,SCBANK      IS BANK NUMBER THE SAME
         BNE   BLDSB2              YES
         CP    SVACCTR,SCACCT      IS ACCOUNT NUMBER THE SAME
         BNE   BLDSB2              YES
         AP    SVCOUNTR,=P'1'      NUMBER OF STATEMENTS
         B     BLDS1
BLDSB2   EQU   *
         CP    SVCOUNTR,=P'0'      IS THIS FIRST TIME THRU
         BE    BLDSF2              YES
         MVC   SRECLNG,SLENGTH     MOVE IN RECORD LENGTH
         ZAP   SPAGE,SVPAGESR      MOVE PAGE NUMBER
         MVC   SDATE,SVDATER       MOVE REPORT DATE
         MVI   STYPE,C'S'          MOVE RECORD TYPE
         MVI   SSIDE,C'R'          LEFT SIDE
         MVC   SFILLER,BLANKS      CLEAR REMAINDER OF KEY
         MVC   SFIRST,SVFIRSTR
         ZAP   SBANK,SVBANKR
         ZAP   SACCOUNT,SVACCTR
         ZAP   SCOUNT,SVCOUNTR
         PUT   IDXOUT,SRECORD
BLDSF2   ZAP   SVBANKR,SCBANK
         ZAP   SVACCTR,SCACCT
         ZAP   SVCOUNTR,=P'1'
         MVC   SVPAGESR,APPAGES
         MVC   SVDATER,XDATE
         MVI   SVFIRSTR,C'Y'
         CLC   DLINE+133+125(4),=C'COPY'      IS THIS A COPY
         BE    BLDSF22
         CLC   DLINE+133+133+125(4),=C'PAGE'  IS THIS FIRST PAGE
         BNE   BLDS1
BLDSF22  MVI   SVFIRSTR,C'N'
BLDS1    EQU   *
         SPACE
         L     R14,BLDSRC14
         BR    R14
         SPACE
BLSMVC1  PACK  SCBANK,0(0,R8)
BLSMVC2  PACK  SCACCT,WORKARE
BLSCLC1  CLC   0(0,R8),ZERO
BLSCLC2  CLC   0(0,R8),NINES
BLDSRC14 DS    F
         EJECT
*----------------------------------------------------------------------
* ROUTINE TO WRITE THE LAST TWO TYPE 'S' INDEX RECORDS ----------------
*----------------------------------------------------------------------
WTRRECS  ST    R14,BLDSRC14
         SPACE
         MVC   SRECLNG,SLENGTH     MOVE IN RECORD LENGTH
         ZAP   SPAGE,SVPAGESL      MOVE PAGE NUMBER
         MVC   SDATE,SVDATEL       MOVE REPORT DATE
         MVI   STYPE,C'S'          MOVE RECORD TYPE
         MVI   SSIDE,C'L'          LEFT SIDE
         MVC   SFILLER,BLANKS      CLEAR REMAINDER OF KEY
         MVC   SFIRST,SVFIRSTL
         ZAP   SBANK,SVBANKL
         ZAP   SACCOUNT,SVACCTL
         ZAP   SCOUNT,SVCOUNTL
         PUT   IDXOUT,SRECORD
         SPACE
         MVC   SRECLNG,SLENGTH     MOVE IN RECORD LENGTH
         ZAP   SPAGE,SVPAGESR      MOVE PAGE NUMBER
         MVC   SDATE,SVDATER       MOVE REPORT DATE
         MVI   STYPE,C'S'          MOVE RECORD TYPE
         MVI   SSIDE,C'R'          LEFT SIDE
         MVC   SFILLER,BLANKS      CLEAR REMAINDER OF KEY
         MVC   SFIRST,SVFIRSTR
         ZAP   SBANK,SVBANKR
         ZAP   SACCOUNT,SVACCTR
         ZAP   SCOUNT,SVCOUNTR
         PUT   IDXOUT,SRECORD
         SPACE
         L     R14,BLDSRC14
         BR    R14
         EJECT
*----------------------------------------------------------------------
* ANALYZE ROUTINE -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
ANALYZE  ST    R14,ANALZ14
         SPACE
         L     R3,SAVEREC         RESTORE SYSOUT RECORD ADDRESS
         CP    LINES,=P'88'       HAVE WE REACHED OUR MAX LINE COUNT
         BE    FINISH             YES, THEN GET OUT
         ZAP   DWD,LINES            NUMBER OF LINES THIS PAGE SO FAR
         CVB   R6,DWD
         L     R5,=F'133'          COMPUTE THE DISPLACMENT INTO
         SR    R4,R4                    RECORD FOR NEXT LINES RECORD
         MR    R4,R6
         LA    R7,DLINE              LOAD ADDRESS OF FIRST LINE
         AR    R7,R5
         MVC   0(133,R7),0(R3)    MOVE SYSOUT LINE
         AP    LINES,=P'1'
         MVI   FIRST,C' '
         CLC   1(132,R7),BLANKS    DID LINE HAVE ANY THING ON IT
         BE    FINISH              BRANCH IF NOT
         MVI   WRTPAGE,C'X'        SET PAGE TO BE WRITTEN OUT
         SPACE
FINISH   L     R14,ANALZ14
         BR    R14
         SPACE
ANALZ14  DS    F
         EJECT
*----------------------------------------------------------------------
* GETDATE ROUTINE -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
GETDATE  ST    R14,DATE14
         SPACE
         LR    R4,R1
         ZAP   PJDAY,=P'0'
         ZAP   PYEAR,=P'0'
         ZAP   TJD,=P'0'
         STCM  R4,R3,PJDAY+6
         STCM  R4,R4,BYTE
         MVO   PYEAR,BYTE
         SR    R4,R4
         CVB   R5,PYEAR
         D     R4,=F'4'
         LTR   R4,R4
         BNZ   NOLEAP
         MVC   DTABLE+6(2),=PL2'29'
         MVI   RESET+1,X'00'
NOLEAP   LA    R4,DTABLE
         LA    R5,12
DLOOP    AP    TJD,2(2,R4)
         CP    TJD,PJDAY+6(2)
         BNL   COMPDATE
         LA    R4,4(R0,R4)
         BCT   R5,DLOOP
DATERR   MVC   DAY,=C'??'
         MVC   MTH,=C'??'
         MVC   YEAR,=C'??'
         B     RESET
         SPACE
COMPDATE SP    TJD,2(2,R4)
         SP    PJDAY,TJD
         UNPK  DAY,PJDAY+6(2)
         OI    DAY+1,X'F0'
         MVC   MTH,0(R4)
         UNPK  YEAR(2),PYEAR+6(2)
         OI    YEAR+1,X'F0'
         SPACE
RESET    B     DATEXIT
         MVI   *-3,X'F0'
         MVC   DTABLE+6(2),=PL2'28'
         SPACE
DATEXIT  L     R14,DATE14
         BR    R14
         SPACE
DATE14   DS    F
         EJECT
*----------------------------------------------------------------------
* ADD/UPDATE  WORK1 FILE WITH BRANCH LEVEL TITLE ID RECORDS
*----------------------------------------------------------------------
         SPACE
ADDBRCH  ST    R14,TIT14
         SPACE
         CP    BRCHNUM,=P'0'       IS THERE A BRANCH NUMBER
         BE    ADDEXIT             BRANCH IF NOT
         L     R2,R2SAVE           RESTORE TITLE RECORD ADDRESS
         CLI   PGNOT,C'S'          IS THIS SECONDARY REPORT ID
         BE    ADDSECD
         CLC   PTID,RTID           TITLE ID MATCH
         BNE   NOGOT               NO, THEN READ IT
         CLC   PRID,RRID           REPORT ID MATCH
         BNE   NOGOT               NO, THEN READ IT
         B     ADDNEXT             TRY OTHER'S
ADDSECD  CLC   PTID,TTID           TITLE ID MATCH
         BNE   NOGOT               NO, THEN READ IT
         CLC   PRID,TRID           REPORT ID MATCH
         BNE   NOGOT               NO, THEN READ IT
ADDNEXT  CLC   PPDATE,XDATE        DOES DATE MATCH
         BNE   NOGOT               NO, THEN READ IT
         CP    PBANK,BANKNUM       BANK MATCH
         BNE   NOGOT               NO, THEN READ IT
         CP    PBRANCH,BRCHNUM     DOES BRANCH MATCH
         BNE   NOGOT               NO, THEN READ IT
         AP    PPPAGES,=P'1'       BUMP PAGE COUNT
         AP    PPLINES,LINES       ADD NUMBER OF LINES
         MVI   WRTBRCH,C'Y'        SET UPDATE NEEDED FLAG ON
         B     ADDEXIT             GOT IT THEN GET OUT
NOGOT    CLI   FRWRT,C'F'          IS THIS FIRST RECORD
         BNE   NOPUT               YES,  THEN DO NO PUT
         PUT   RPL=GETWRK          UPDATE OLD RECORD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR06             NO, THEN ERROR
         MVI   WRTBRCH,C' '        SET UPDATE NEEDED FLAG OFF
NOPUT    CLI   PGNOT,C'S'          IS THIS SECONDARY REPORT ID
         BE    ADDSEC1
         MVC   PTID,RTID           MOVE TITLE ID
         MVC   PRID,RRID           MOVE REPORT ID
         MVC   PPTTIT,RPTTIT       MOVE TITLE NAME
         B     ADDNXT
ADDSEC1  MVC   PTID,TTID           MOVE TITLE ID
         MVC   PRID,TRID           MOVE REPORT ID
         MVC   PPTTIT,TPTTIT       MOVE TITLE NAME
ADDNXT   MVC   PPDATE,XDATE        MOVE REPORT DATE
         MVI   PTYPE,C'T'          MOVE INDEX TITLE TYPE
         MVI   FRWRT,C'F'          MOVE INDEX TITLE TYPE
         ZAP   PBANK,BANKNUM       MOVE BANK NUMBER
         ZAP   PBRANCH,BRCHNUM     MOVE BRANCH NUMBER
         MVC   PFILLER,BLANKS
         GET   RPL=GETWRK          READ RECORD
         LTR   R15,R15             DID WE GET A RECORD
         BNZ   CHKERR              NO, CHECK FOR NO RECORD FOUND
         AP    PPPAGES,=P'1'       BUMP PAGE COUNT
         AP    PPLINES,LINES       ADD NUMBER OF LINES
         MVC   PFILL1,BLANKS
         MVI   WRTBRCH,C'Y'        SET UPDATE NEEDED FLAG ON
         B     ADDEXIT             GET OUT
CHKERR   C     R15,=F'08'          NO RECORD FOUND RETURN CODE
         BE    CHKNOR              BRANCH IF 8
         B     ERROR10
NOREC    ZAP   PPPAGES,=P'1'       BUMP PAGE COUNT
         ZAP   PPLINES,LINES       ADD NUMBER OF LINES
         MVC   PFILL1,BLANKS
         MODCB RPL=GETWRK,OPTCD=NUP
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR09             NO, THEN ERROR
         PUT   RPL=GETWRK          ADD NEW RECORD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR07             NO, THEN ERROR
         MODCB RPL=GETWRK,OPTCD=UPD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR09             NO, THEN ERROR
         GET   RPL=GETWRK          READ RECORD
         LTR   R15,R15             DID WE GET A RECORD
         BNZ   ERROR10             NO, CHECK FOR NO RECORD FOUND
         B     ADDEXIT
CHKNOR   EQU   *
         SHOWCB RPL=GETWRK,AREA=FEEDBK,FIELDS=FDBK,LENGTH=4
         LTR   R15,R15             DID SHOW WORK
         BNZ   ERROR09             NO, THENGET OUT
         CLI   ERRCD,16            DID WE GET A NO RECORD FOUND
         BE    NOREC               YES, THEN OK ADD IT
         B     ERROR05
         SPACE
ADDEXIT  L     R14,TIT14
         BR    R14
         EJECT
*----------------------------------------------------------------------
* ADD/UPDATE  WORK2 FILE WITH BANK LEVEL TITLE ID RECORDS
*----------------------------------------------------------------------
         SPACE
ADDBANK  ST    R14,TIT14
         SPACE
         L     R2,R2SAVE           RESTORE TITLE RECORD ADDRESS
         CLI   PGNOT,C'S'          IS THIS SECONDARY REPORT ID
         BE    ADDSECD1
         CLC   BTID,RTID           TITLE ID MATCH
         BNE   NOGOT1              NO, THEN READ IT
         CLC   BRID,RRID           REPORT ID MATCH
         BNE   NOGOT1              NO, THEN READ IT
         B     ADDNEXT1            TRY OTHER'S
ADDSECD1 CLC   BTID,TTID           TITLE ID MATCH
         BNE   NOGOT1              NO, THEN READ IT
         CLC   BRID,TRID           REPORT ID MATCH
         BNE   NOGOT1              NO, THEN READ IT
ADDNEXT1 CLC   BPDATE,XDATE        DOES DATE MATCH
         BNE   NOGOT1              NO, THEN READ IT
         CP    BBANK,BANKNUM       BANK MATCH
         BNE   NOGOT1              NO, THEN READ IT
         AP    BPPAGES,=P'1'       BUMP PAGE COUNT
         AP    BPLINES,LINES       ADD NUMBER OF LINES
         MVI   WRTBNK,C'Y'         SET UPDATE NEEDED FLAG ON
         B     ADDEXIT1            GOT IT THEN GET OUT
NOGOT1   CLI   BRWRT,C'F'          IS THIS FIRST RECORD
         BNE   NOPUT1              YES,  THEN DO NO PUT
         PUT   RPL=GETWRK2         UPDATE OLD RECORD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR06             NO, THEN ERROR
         MVI   WRTBNK,C' '         SET UPDATE NEEDED FLAG OFF
NOPUT1   CLI   PGNOT,C'S'          IS THIS SECONDARY REPORT ID
         BE    ADDSEC11
         MVC   BTID,RTID           MOVE TITLE ID
         MVC   BRID,RRID           MOVE REPORT ID
         MVC   BPTTIT,RPTTIT       MOVE TITLE NAME
         B     ADDNXT1
ADDSEC11 MVC   BTID,TTID           MOVE TITLE ID
         MVC   BRID,TRID           MOVE REPORT ID
         MVC   BPTTIT,TPTTIT       MOVE TITLE NAME
ADDNXT1  MVC   BPDATE,XDATE        MOVE REPORT DATE
         MVI   BTYPE,C'T'          MOVE INDEX TITLE TYPE
         MVI   BRWRT,C'F'          MOVE INDEX TITLE TYPE
         ZAP   BBANK,BANKNUM       MOVE BANK NUMBER
         ZAP   BBRANCH,=P'0'       MOVE 0 BRANCH NUMBER
         MVC   BFILLER,BLANKS
         GET   RPL=GETWRK2         READ RECORD
         LTR   R15,R15             DID WE GET A RECORD
         BNZ   CHKERR1             NO, CHECK FOR NO RECORD FOUND
         AP    BPPAGES,=P'1'       BUMP PAGE COUNT
         AP    BPLINES,LINES       ADD NUMBER OF LINES
         MVC   BFILL1,BLANKS
         MVI   WRTBNK,C'Y'         SET UPDATE NEEDED FLAG ON
         B     ADDEXIT1            GET OUT
CHKERR1  C     R15,=F'08'          NO RECORD FOUND RETURN CODE
         BE    CHKNOR1             BRANCH IF 8
         B     ERROR10
NOREC1   ZAP   BPPAGES,=P'1'       BUMP PAGE COUNT
         ZAP   BPLINES,LINES       ADD NUMBER OF LINES
         MVC   BFILL1,BLANKS
         MODCB RPL=GETWRK2,OPTCD=NUP
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR09             NO, THEN ERROR
         PUT   RPL=GETWRK2         ADD NEW RECORD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR07             NO, THEN ERROR
         MODCB RPL=GETWRK2,OPTCD=UPD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR09             NO, THEN ERROR
         GET   RPL=GETWRK2         READ RECORD
         LTR   R15,R15             DID WE GET A RECORD
         BNZ   ERROR10             NO, CHECK FOR NO RECORD FOUND
         B     ADDEXIT1
CHKNOR1  EQU   *
         SHOWCB RPL=GETWRK2,AREA=FEEDBK,FIELDS=FDBK,LENGTH=4
         LTR   R15,R15             DID SHOW WORK
         BNZ   ERROR09             NO, THEN GET OUT
         CLI   ERRCD,16            DID WE GET A NO RECORD FOUND
         BE    NOREC1              YES, THEN OK ADD IT
         B     ERROR05
         SPACE
ADDEXIT1 L     R14,TIT14
         BR    R14
         EJECT
*----------------------------------------------------------------------
* CHECK CURRENT INDEX FILE FOR DUPLICATE REPORT ID & DATE
*----------------------------------------------------------------------
         SPACE
CHECKOLD ST    R14,TIT14
         SPACE
         L     R2,R2SAVE           RESTORE TITLE RECORD ADDRESS
         CLC   XTID,RTID           TITLE ID MATCH
         BNE   NOXGET              NO, THEN READ IT
         CLC   XIDATE,OLDDATE      DOES DATE MATCH
         BNE   NOXGET              NO, THEN READ IT
         CP    XBANK,BANKNUM       DO BANK NUMBERS MATCH
         BNE   NOXGET              NO, THEN READ IT
         B     CHECKEXT            GOT IT THEN GET OUT
NOXGET   MVC   XIDATE,XDATE        MOVE REPORT DATE
         MVC   XTID,RTID           MOVE REPORT ID
         MVI   XTYPE,C'T'          MOVE INDEX RECORD TYPE
         ZAP   XBANK,BANKNUM       MOVE BANK NUMBER
         ZAP   XBRANCH,=P'0'       MAKE BRANCH NUMBER ZERO
         MVC   XFILLER,BLANKS
         MVC   XRID,BLANKS         MAKE SECONDRARY ID SPACES
         GET   RPL=GETIDX          READ RECORD
         LTR   R15,R15             DID WE GET A RECORD
         BNZ   CHKERR4             NO, CHECK FOR NO RECORD FOUND
         MVI   IDXHIT,C'Y'         WE GOT A HIT, DO NOT EXTRACT
         B     CHECKEXT            GET OUT
CHKERR4  C     R15,=F'08'          NO RECORD FOUND RETURN CODE
         BE    CHKNOR4             BRANCH IF 8
         B     ERROR11
NOREC4   MVI   IDXHIT,C'N'         NO HIT, EXTRACT IT
         B     CHECKEXT            GET OUT
CHKNOR4  EQU   *
         SHOWCB RPL=GETIDX,AREA=FEEDBK,FIELDS=FDBK,LENGTH=4
         LTR   R15,R15             DID SHOW WORK
         BNZ   ERROR12             NO, THEN GET OUT
         CLI   ERRCD,16            DID WE GET A NO RECORD FOUND
         BE    NOREC4              YES, THEN OK ADD IT
         B     ERROR14
         SPACE
CHECKEXT L     R14,TIT14
         BR    R14
         EJECT
*----------------------------------------------------------------------
* CHECK FOR A PARTIAL EXTRACT BY BANK
*----------------------------------------------------------------------
         SPACE
CHECKBNK ST    R14,TIT14
         SPACE
         L     R2,R2SAVE           RESTORE TITLE RECORD ADDRESS
         MVI   BNKHIT,C'N'         DO NOT EXTRACT DEFAULT
         MVI   SBNKHIT,C'N'        DO NOT EXTRACT
         CLI   RPTEXT,C'Y'         DO A FULL EXTRACT
         BE    OKDOIT              YES, THEN DO THIS ONE
         CLI   RPTEXT,C'P'         DO A PARTIAL EXTRACT
         BE    BANKCHK             YES, THEN CHECK BANK TO EXTRACT
         B     BANKEXT             GET OUT
BANKCHK  CP    BANKNUM,RPTBNK      DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+2    DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+4    DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+6    DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+8    DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+10   DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+12   DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+14   DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+16   DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         CP    BANKNUM,RPTBNK+18   DOES BANK NUMBER MATCH
         BE    OKDOIT              YES, THEN EXTRACT IT
         B     BANKSCHK            GET OUT
OKDOIT   MVI   BNKHIT,C'Y'         HIT, EXTRACT IT
         B     BANKSCHK            GET OUT
BANKSCHK L     R2,R2SAVE
         L     R3,SAVERID       LOAD ADDRESS OF REPORT TABLE
CHKRBNK  CLC   TTID,RTID           DO TITLE ID'S MATCH
         BE    BNKRHIT             YES
TRYAGNB  CLI   0(R3),X'FF'         IS THIS END OF TABLE
         BE    BANKEXT             YES, THEN GET OUT
         LA    R3,REPT3(R3)        BUMP TO NEXT RECORD
         B     CHKRBNK             GET NEXT TABLE
BNKRHIT  LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,TPTTL1          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPTRP1          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,TPTRL1            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCRT1                     IS THIS OUR TITLE?
         BE    BNKRHIT1                      YES,  WE HAVE A HIT
         B     TRYAGNB             NO THEN TRY AGAIN
BNKRHIT1 CP    TPTRL2,=P'0'        IS THERE A LENGTH
         BE    BNKRHIT2            YES,  WE HAVE ONE
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,TPTTL2          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPTRP2          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,TPTRL2            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCRT2                     IS THIS OUR TITLE?
         BE    BNKRHIT2                      YES,  WE HAVE A HIT
         B     TRYAGNB             NO THEN TRY AGAIN
BNKRHIT2 CP    TPTRL3,=P'0'        IS THERE A LENGTH
         BE    OKHITBNK            NO,  WE DON'T HAVE ONE
         LA    R8,DLINE            FIRST SYSOUT LINE ON THIS PAGE
         ZAP   DWD,TPTTL3          NO, SETUP TO ADD
         BAL   R14,CNVHEX         CONVERT TO HEX AND ADD
         ZAP   DWD,TPTRP3          SETUP TO ADD
         CVB   R4,DWD                TITLE POSITION
         BCTR  R4,0
         AR    R8,R4                POINT TO WHERE TITLE WOULD BE
         ZAP   DWD,TPTRL3            CONVERT TITLE LINE
         CVB   R4,DWD                        LENGTH TO HEX
         BCTR  R4,0                          FOR COMPARE
         EX    R4,CLCRT3                     IS THIS OUR TITLE?
         BE    OKHITBNK                      YES,  WE HAVE A HIT
         B     TRYAGNB             NO THEN TRY AGAIN
OKHITBNK CLI   TPTEXT,C'Y'         FULL EXTRACT
         BE    OKDOITS             YES, EXTRACT IT
         CLI   TPTEXT,C'P'         PARTIAL  EXTRACT
         BE    BRCHHIT             YES, EXTRACT IT
         B     TRYAGNB             NO THEN TRY AGAIN
BRCHHIT  CP    BANKNUM,TPTBNK      DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+2    DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+4    DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+6    DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+8    DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+10   DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+12   DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+14   DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+16   DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         CP    BANKNUM,TPTBNK+18   DOES BANK NUMBER MATCH
         BE    OKDOITS             YES, THEN EXTRACT IT
         B     TRYAGNB             NO THEN TRY AGAIN
OKDOITS  MVI   SBNKHIT,C'Y'        YES WE HAVE A SECONDARY HIT
         B     BANKEXT
BANKEXT  L     R14,TIT14
         BR    R14
         EJECT
*----------------------------------------------------------------------
* ADD/UPDATE/GET WORK3 FILE WITH OVERALL REPORT ID DATA
*----------------------------------------------------------------------
         SPACE
GETREPT  ST    R14,TIT14
         SPACE
         L     R2,R2SAVE           RESTORE TITLE RECORD ADDRESS
         CLI   PGNOT,C'S'          IS THIS SECONDARY REPORT ID
         BE    GETSECD
         CLC   ATID,RTID           TITLE ID MATCH
         BNE   NOAGET              NO, THEN READ IT
         CLC   ARID,RRID           REPORT ID MATCH
         BNE   NOAGET              NO, THEN READ IT
         B     GETNEXT             TRY OTHER'S
GETSECD  CLC   ATID,TTID           TITLE ID MATCH
         BNE   NOAGET              NO, THEN READ IT
         CLC   ARID,TRID           REPORT ID MATCH
         BNE   NOAGET              NO, THEN READ IT
GETNEXT  CLC   APDATE,XDATE        DOES DATE MATCH
         BNE   NOAGET              NO, THEN READ IT
         AP    APPAGES,=P'1'       BUMP PAGE COUNT
         MVI   WRTOVR,C'Y'         SET UPDATE NEEDED FLAG ON
         B     REPTEXT             GOT IT THEN GET OUT
NOAGET   CLI   ARWRT,C'F'          IS THIS FIRST RECORD
         BNE   NOGET1              YES,  THEN DO NO PUT
         PUT   RPL=GETWRK3         UPDATE OLD RECORD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR06             NO, THEN ERROR
         MVI   WRTOVR,C' '         SET UPDATE NEEDED FLAG OFF
NOGET1   CLI   PGNOT,C'S'          IS THIS SECONDARY REPORT ID
         BE    GETSEC1
         MVC   ATID,RTID           MOVE TITLE ID
         MVC   ARID,RRID           MOVE REPORT ID
         B     GETNXT
GETSEC1  MVC   ATID,TTID           MOVE TITLE ID
         MVC   ARID,TRID           MOVE REPORT ID
GETNXT   MVC   APDATE,XDATE        MOVE REPORT DATE
         MVI   ARWRT,C'F'          MOVE INDEX TITLE TYPE
         GET   RPL=GETWRK3         READ RECORD
         LTR   R15,R15             DID WE GET A RECORD
         BNZ   CHKERR3             NO, CHECK FOR NO RECORD FOUND
         AP    APPAGES,=P'1'       BUMP PAGE COUNT
         MVI   WRTOVR,C'Y'         SET UPDATE NEEDED FLAG ON
         B     REPTEXT             GET OUT
CHKERR3  C     R15,=F'08'          NO RECORD FOUND RETURN CODE
         BE    CHKNOR3             BRANCH IF 8
         B     ERROR10
NOREC3   ZAP   APPAGES,=P'1'       BUMP PAGE COUNT
         MODCB RPL=GETWRK3,OPTCD=NUP
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR09             NO, THEN ERROR
         PUT   RPL=GETWRK3         ADD NEW RECORD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR07             NO, THEN ERROR
         MODCB RPL=GETWRK3,OPTCD=UPD
         LTR   R15,R15             DID IT WORK
         BNZ   ERROR09             NO, THEN ERROR
         GET   RPL=GETWRK3         READ RECORD
         LTR   R15,R15             DID WE GET A RECORD
         BNZ   ERROR10             NO, CHECK FOR NO RECORD FOUND
         B     REPTEXT
CHKNOR3  EQU   *
         SHOWCB RPL=GETWRK3,AREA=FEEDBK,FIELDS=FDBK,LENGTH=4
         LTR   R15,R15             DID SHOW WORK
         BNZ   ERROR09             NO, THEN GET OUT
         CLI   ERRCD,16            DID WE GET A NO RECORD FOUND
         BE    NOREC3              YES, THEN OK ADD IT
         B     ERROR05
         SPACE
REPTEXT  L     R14,TIT14
         BR    R14
         SPACE
RPT14    DS    F
TIT14    DS    F
         EJECT
*----------------------------------------------------------------------
* EOJ ROUTINE ---------------------------------------------------------
*----------------------------------------------------------------------
ERROR01  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         MVC   HCARD+1(L'EMSG01),EMSG01
         B     ERRORPT
ERROR02  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         MVC   HCARD+1(L'EMSG02),EMSG02
         B     ERRORPT
ERROR03  EQU   *
         LA    R15,16
         ST    R15,RETCD
         BAL   R7,LINECLR
         MVC   HCARD+1(L'EMSG03),EMSG03
         B     ERRORPT
ERROR04  EQU   *
         LA    R15,16
         ST    R15,RETCD
         BAL   R7,LINECLR
         MVC   HCARD+1(L'EMSG04),EMSG04
         B     ERRORPT
ERROR05  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG05),EMSG05
         B     ERRORPT
ERROR06  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG06),EMSG06
         B     ERRORPT
ERROR07  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG07),EMSG07
         B     ERRORPT
ERROR08  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG08),EMSG08
         B     ERRORPT
ERROR09  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG09),EMSG09
         B     ERRORPT
ERROR10  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG10),EMSG10
         B     ERRORPT
ERROR11  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG11),EMSG11
         B     ERRORPT
ERROR12  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG12),EMSG12
         B     ERRORPT
ERROR14  EQU   *
         ST    R15,RETCD
         BAL   R7,LINECLR
         BAL   R7,GETFEED
         MVC   HCARD+1(L'EMSG14),EMSG14
         B     ERRORPT
ERROR15  EQU   *
         LA    R15,16
         ST    R15,RETCD
         BAL   R7,LINECLR
         MVC   HCARD+1(L'EMSG15),EMSG15
         B     ERRORPT
ERRORPT  OPEN  (SYSOUT,(OUTPUT))
         LTR   R15,R15
         BZ    ERRPRT
         ST    R15,RETCD
         WTO   'OPEN ERROR ON SYSOUT FILE'
         B     EOJ
ERRPRT   MVI   CARD,C'*'
         MVC   CARD+1(132),CARD
         PUT   SYSOUT,CARD
         PUT   SYSOUT,CARD
         MVC   HCARD+70(L'PAGEPAT),PAGEPAT
         ED    HCARD+70(L'PAGEPAT),DWD+5
         PUT   SYSOUT,HCARD
         PUT   SYSOUT,CARD
         PUT   SYSOUT,CARD
         CLOSE SYSOUT
         B     EOJ
GETFEED  EQU   *
         SHOWCB RPL=GETWRK,AREA=FEEDBK,FIELDS=FDBK,LENGTH=4
         LTR   R15,R15             DID SHOW WORK
         BNZ   ERROR09             NO, THENGET OUT
         SR    R6,R6
         IC    R6,ERRCD
         CVD   R6,DWD
         BR    R7
         SPACE
EOJ      EQU   *
         SPACE
EARLYOUT EQU   *
         CLOSE (RPTIN)
         CLOSE (RPTOUT)
         CLOSE (IDXOUT)
         CLOSE (WORK1)
         CLOSE (WORK2)
         CLOSE (WORK3)
         CLOSE (INDEX)
         SPACE
EXIT     DS    0H
         L     R15,RETCD
         L     13,4(13)
         RETURN (14,12),RC=(15)
         EJECT
*----------------------------------------------------------------------
* SUB ROUTINES --------------------------------------------------------
*----------------------------------------------------------------------
         SPACE
         SPACE
*----------------------------------------------------------------------
LINECLR  MVI   HCARD,C' '
         MVC   HCARD+1(132),HCARD
         BR    R7
         SPACE
         EJECT
*----------------------------------------------------------------------
* PROGRAM VARS CONSTANTS AND TABLES -----------------------------------
*----------------------------------------------------------------------
         SPACE
EMSG01   DC    C'ERROR CLOSING TITLES FILE'
EMSG02   DC    C'ERROR READING TITLES FILE'
EMSG03   DC    C'ERROR TITLE ID RECORD TABLE FULL'
EMSG04   DC    C'ERROR REPORT ID RECORD TABLE FULL'
EMSG05   DC    C'ERROR READING WORK1 FILE'
EMSG06   DC    C'ERROR UPDATING WORK1 FILE'
EMSG07   DC    C'ERROR ADDING TO WORK1 FILE'
EMSG08   DC    C'ERROR READING WORK1 FILE SEQUENTIALLY'
EMSG09   DC    C'ERROR DOING SHOWCB ON WORK1 FILE'
EMSG10   DC    C'ERROR READING WORK1 FILE KEY ACCESS'
EMSG11   DC    C'ERROR READING INDEX FILE KEY ACCESS'
EMSG12   DC    C'ERROR DOING SHOWCB ON INDEX FILE'
EMSG14   DC    C'ERROR READING INDEX FILE'
EMSG15   DC    C'ERROR NOT ENOUGH SYSIN DATA CARDS'
SAVEREC  DS    F
SAVETIT  DS    F
SAVERID  DS    F
R2SAVE   DS    F
R3SAVE   DS    F
R5SAVE   DS    F
DRECMAX  DC    F'12000'
DWD      DS    D
FWD      DS    F
HWD      DS    H
         SPACE
         DS    0D
EORADD   DC    F'0'
WKAREA   DC    F'0'
RETCD    DC    F'0'
         SPACE
BANKNUM  DC    PL2'0'
BRCHNUM  DC    PL2'0'
RCDATE   DC    CL6'   '
SCDATE   DC    CL6'   '
XDATE    DC    CL6'   '
IDXHIT   DC    CL1' '
BNKHIT   DC    CL1' '
SBNKHIT  DC    CL1' '
BYTE     DC    CL1' '
ZEROS    DC    4X'0'
SYSINREC DS    0CL80         SYSIN CARD
         DS    CL1          TYPE OF EXTRACT
APPLID   DS    CL3
         DS    CL77      DO NOT PUT ANY OTHER THING BETWEEN THESE 2
BLANKS   DC    133C' '   ------
ZERO     DC    20C'0'
NINES    DC    20C'9'
WORKARE  DC    40C' '
         SPACE
         DS    0D
PJDAY    DC    PL8'0'
PYEAR    DC    PL8'0'
TJD      DC    PL4'0'
TODAY    DC    CL8' '
         SPACE
HLDDATE  DS    0CL8
MTH      DC    CL2' '
         DC    CL1'/'
DAY      DC    CL2' '
         DC    CL1'/'
YEAR     DC    CL2' '
         SPACE
RPTDATE  DS    0CL8
MTHRPT   DC    CL2' '
         DC    CL1'/'
DAYRPT   DC    CL2' '
         DC    CL1'/'
YEARRPT  DC    CL2' '
         SPACE
         DS    0F
CARD     DS    0CL133
         DS    CL1
PREPORT  DS    CL8
         DS    CL4
PSID     DS    CL8
         DS    CL5
PDTE     DS    CL8
         DS    CL8
PNAME    DS    CL50
         DS    CL1
PPAGES   DS    CL11
         DS    CL4
PLINES   DS    CL12
         DS    CL20
         SPACE
TOTTBL   DS    0F
TOTPAGES DC    PL5'0'       TOTAL PAGES READ
TOTPGS   DC    PL5'0'       TOTAL PAGES OUTPUT
TOTLNS   DC    PL5'0'       TOTAL LINES OUTPUT
TOTLINES DC    PL5'0'       TOTAL LINES READ
LINES    DC    PL2'0'       TOTAL LINES ON THIS PAGE
         SPACE
         DS    0D
WRTPAGE  DC    C' '
OLDDATE  DC    CL6' '
BADDATE  DC    C' '
FIRST    DC    C'X'
PGNOT    DC    C'X'
FRWRT    DC    C'X'
ARWRT    DC    C'X'
BRWRT    DC    C'X'
WRTBRCH  DC    C' '
WRTBNK   DC    C' '
WRTOVR   DC    C' '
FEEDBK   DS    0F
         DS    1C
TYPECD   DS    1C
CMPCD    DS    1C
ERRCD    DS    1C
DDASTMT  DC    C'N'
SCBANK   DC    PL4'0'
SCACCT   DC    PL6'0'
SVBANKL  DC    PL4'0'
SVACCTL  DC    PL6'0'
SVCOUNTL DC    PL3'0'
SVPAGESL DC    PL4'0'
SVDATEL  DC    CL6'0'
SVFIRSTL DC    C' '
SVFIRSTR DC    C' '
SVPAGESR DC    PL4'0'
SVDATER  DC    CL6'0'
SVBANKR  DC    PL4'0'
SVACCTR  DC    PL6'0'
SVCOUNTR DC    PL3'0'
         DS    0D
LINECNT  DC    PL3'50'
PAGECNT  DC    PL3'0'
SLENGTH  DC    H'39'         RECORD LENGTH FOR TYPE 'S' INDEX RECORD
TLENGTH  DC    H'100'        RECORD LENGTH FOR TYPE 'T' INDEX RECORD
ILENGTH  DC    H'61'         RECORD LENGTH FOR TYPE 'I' INDEX RECORD
KLENGTH  DC    H'61'         RECORD LENGTH FOR TYPE 'K' INDEX RECORD
DLENGTH  DC    H'24'    FIXED PART OF RECORD LENGTH FOR DETAIL RECORD
         SPACE
PAGEPAT  DC    X'402020202020'
EDWRD1   DC    X'40206B2020206B202020'
EDWRD2   DC    X'402020206B2020206B202020'
SAVE14   DC    F'0'
KSAVE    DC    F'0'
TSAVE    DC    F'0'
SSAVE    DC    F'0'
RPTSAVE  DC    F'0'
RPTSAVE1 DC    F'0'
RECADDR  DC    F'0'
         SPACE
DTABLE   DC    CL2'01',PL2'31',CL2'02',PL2'28',CL2'03',PL2'31'
         DC    CL2'04',PL2'30',CL2'05',PL2'31',CL2'06',PL2'30'
         DC    CL2'07',PL2'31',CL2'08',PL2'31',CL2'09',PL2'30'
         DC    CL2'10',PL2'31',CL2'11',PL2'30',CL2'12',PL2'31'
         SPACE
         DS    0F
HCARD    DS    CL133
         EJECT
*----------------------------------------------------------------------
* FILE DCB, ACB AND RPL'S  --------------------------------------------
*----------------------------------------------------------------------
         SPACE
CNTLFILE DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               RECFM=FB,                                               X
               LRECL=80,                                               X
               BLKSIZE=80,                                             X
               MACRF=(GL),                                             X
               EODAD=CNTLEOF
         SPACE
RPTIN    DCB   DDNAME=RPTIN,                                           X
               DSORG=PS,                                               X
               RECFM=FB,                                               X
               LRECL=133,                                              X
               MACRF=(GL),                                             X
               EODAD=RPTEOF
         SPACE
RPTOUT   DCB   DDNAME=RPTOUT,                                          X
               DSORG=PS,                                               X
               RECFM=VB,                                               X
               MACRF=(PM)
         SPACE
IDXOUT   DCB   DDNAME=IDXOUT,                                          X
               DSORG=PS,                                               X
               RECFM=VB,                                               X
               MACRF=(PM)
         SPACE
SYSOUT   DCB   DDNAME=SYSOUT,                                          X
               DSORG=PS,                                               X
               RECFM=FBA,                                              X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               MACRF=(PM)
         SPACE
****************************
* TITLE FILE ACB'S AND RPL'S
****************************
         SPACE
TITLES   ACB   DDNAME=TITLES,                                          X
               AM=VSAM,CATALOG=NO,EXLST=ERROR,                         X
               MACRF=(KEY,NFX,NDF,SEQ,NCI,IN,NIS,NRM,NRS,NUB)
         SPACE
ERROR    EXLST AM=VSAM,EODAD=TITEOF
         SPACE
GETRPL   RPL   ACB=TITLES,AM=VSAM,AREA=RECADDR,AREALEN=4,              X
               OPTCD=(KEY,SEQ,ARD,FWD,SYN,NUP,LOC)
         SPACE
**********************************************************************
* WORK1 FILE ACB'S AND RPL'S  FOR INDEX FILE TITLE RECORDS
**********************************************************************
         SPACE
*  OPEN VSAM FILE WITH BOTH DIRECT AND SEQUENTIAL PROCESS
*  TO READ, UPDATE, DELETE AND ADD RECORDS
WORK1    ACB   DDNAME=WORK1,                                           X
               AM=VSAM,CATALOG=NO,EXLST=ERROR1,                        X
               MACRF=(KEY,NFX,NDF,DIR,SEQ,NCI,IN,OUT,NIS,NRM,NRS,NUB)
         SPACE
ERROR1   EXLST AM=VSAM,EODAD=NOREC
         SPACE
*  DIRECT READ FOR UPDATE   KEYED FROM RECKEY FIELD
GETWRK   RPL   ACB=WORK1,AM=VSAM,AREA=WORKREC,AREALEN=100,ARG=RECKEY,  X
               OPTCD=(KEY,DIR,KEQ,UPD,FKS,SYN,MVE),                    X
               KEYLEN=31,RECLEN=100
**********************************************************************
* WORK2 FILE ACB'S AND RPL'S  FOR INDEX FILE TITLE RECORDS
**********************************************************************
         SPACE
*  OPEN VSAM FILE WITH BOTH DIRECT AND SEQUENTIAL PROCESS
*  TO READ, UPDATE, DELETE AND ADD RECORDS
WORK2    ACB   DDNAME=WORK2,                                           X
               AM=VSAM,CATALOG=NO,EXLST=ERROR2,                        X
               MACRF=(KEY,NFX,NDF,DIR,SEQ,NCI,IN,OUT,NIS,NRM,NRS,NUB)
         SPACE
ERROR2   EXLST AM=VSAM,EODAD=NOREC1
         SPACE
*  DIRECT READ FOR UPDATE   KEYED FROM RECKEY FIELD
GETWRK2  RPL   ACB=WORK2,AM=VSAM,AREA=BORKREC,AREALEN=100,ARG=BECKEY,  X
               OPTCD=(KEY,DIR,KEQ,UPD,FKS,SYN,MVE),                    X
               KEYLEN=31,RECLEN=100
**********************************************************************
* WORK3 FILE ACB'S AND RPL'S  FOR INDEX FILE TITLE RECORDS
**********************************************************************
         SPACE
*  OPEN VSAM FILE WITH BOTH DIRECT AND SEQUENTIAL PROCESS
*  TO READ, UPDATE, DELETE AND ADD RECORDS
WORK3    ACB   DDNAME=WORK3,                                           X
               AM=VSAM,CATALOG=NO,EXLST=ERROR3,                        X
               MACRF=(KEY,NFX,NDF,DIR,SEQ,NCI,IN,OUT,NIS,NRM,NRS,NUB)
         SPACE
ERROR3   EXLST AM=VSAM,EODAD=NOREC3
         SPACE
*  DIRECT READ FOR UPDATE   KEYED FROM RECKEY FIELD
GETWRK3  RPL   ACB=WORK3,AM=VSAM,AREA=REPTREC,AREALEN=26,ARG=REPTKEY,  X
               OPTCD=(KEY,DIR,KEQ,UPD,FKS,SYN,MVE),                    X
               KEYLEN=22,RECLEN=26
**********************************************************************
* INDEX FILE ACB'S AND RPL'S  FOR CURRENT INDEX FILE RECORDS
**********************************************************************
         SPACE
*  OPEN VSAM FILE WITH DIRECT PROCESS
*  TO READ RECORDS
INDEX    ACB   DDNAME=INDEX,                                           X
               AM=VSAM,CATALOG=NO,EXLST=ERROR4,                        X
               MACRF=(KEY,NFX,NDF,DIR,NCI,IN,NIS,NRM,NRS,NUB)
         SPACE
ERROR4   EXLST AM=VSAM,EODAD=NOREC4
         SPACE
*  DIRECT READ FOR KEYED FROM XIXKEY FIELD
GETIDX   RPL   ACB=INDEX,AM=VSAM,AREA=INDEXREC,AREALEN=100,ARG=XIXKEY, X
               OPTCD=(KEY,DIR,KEQ,FKS,SYN,MVE),                        X
               KEYLEN=31,RECLEN=100
         EJECT
*----------------------------------------------------------------------
* LITERALS FOR BASE1 AND BASE2 ----------------------------------------
*----------------------------------------------------------------------
         SPACE
         LTORG
         EJECT
*----------------------------------------------------------------------
* EQUATES FOR PROGRAM -------------------------------------------------
*----------------------------------------------------------------------
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*----------------------------------------------------------------------
* REPORT INDEX TYPE 'I' RECORD LAYOUT  --------------------------------
*----------------------------------------------------------------------
         SPACE
         DS    0H
IRECORD  DS    0CL61  TYPE 'I' RECORD  LENGTH 61
IRECLNG  DS    H
IRECLNG1 DS    H
ITYPE    DS    CL1
ITITID   DS    CL8
IRIDID   DS    CL8
IDATE    DS    CL6
IBANK    DS    PL2
IBRANCH  DS    PL2
ISEQ     DS    PL4
IPAGE    DS    PL4
IIDXL    DS    PL2
IKEYV    DS    XL20
         SPACE
         DS    0H
KRECORD  DS    0CL61 TYPE 'K' RECORD LENGTH 61
KRECLNG  DS    H
KRECLNG1 DS    H
KTYPE    DS    CL1
KTITID   DS    CL8
KRIDID   DS    CL8  ALWAYS BLANKS
KDATE    DS    CL6
KBANK    DS    PL2
KBRANCH  DS    PL2
KSEQ     DS    PL4
KPAGE    DS    PL4
KIDXL    DS    PL2
KKEYV    DS    CL20
         SPACE
         DS    0H
SRECORD  DS    0CL39     TYPE 'S' RECORD LENGTH 39
SRECLNG  DS    H
SRECLNG1 DS    H
STYPE    DS    CL1
SBANK    DS    PL4
SACCOUNT DS    PL6
SDATE    DS    CL6
SPAGE    DS    PL4
SFIRST   DS    CL1
SFILLER  DS    CL9
SCOUNT   DS    PL3
SSIDE    DS    CL1
         EJECT
*----------------------------------------------------------------------
* REPORT WORK FILES    ------------------------------------------------
*----------------------------------------------------------------------
         SPACE
* WORK1 BRANCH TITLE RECORD LAYOUT
WORKREC  DS    0CL100
RECKEY   DS    0CL31          VSAM RECORD KEY
PTYPE    DC    CL1'T'        TYPE OF INDEX RECORD
PBANK    DC    PL2'0'        BANK
PBRANCH  DC    PL2'0'        BRANCH
PTID     DC    CL8'  '       TITLE ID
PRID     DC    CL8'  '       REPORT ID
PPDATE   DC    CL6'000000'   REPORT DATE
PFILLER  DC    CL4' '        FILLER OF VSAM KEY
PPTTIT   DC    CL50' '       REPORT TITLE NAME
PPPAGES  DC    PL4'0'        PAGES
PPLINES  DC    PL5'0'        LINES
PFILL1   DC    CL10' '       AVALIABLE SPACES
         SPACE
* WORK2 BANK TITLE RECORD LAYOUT
BORKREC  DS    0CL100
BECKEY   DS    0CL31          VSAM RECORD KEY
BTYPE    DC    CL1'T'        TYPE OF INDEX RECORD
BBANK    DC    PL2'0'        BANK
BBRANCH  DC    PL2'0'        BRANCH
BTID     DC    CL8'  '       TITLE ID
BRID     DC    CL8'  '       REPORT ID
BPDATE   DC    CL6'000000'   REPORT DATE
BFILLER  DC    CL4' '        FILLER OF VSAM KEY
BPTTIT   DC    CL50' '       REPORT TITLE NAME
BPPAGES  DC    PL4'0'        PAGES
BPLINES  DC    PL5'0'        LINES
BFILL1   DC    CL10' '       AVALIABLE SPACES
         SPACE
* WORK3 REPORT ID RECORD
REPTREC  DS    0CL26
REPTKEY  DS    0CL22          VSAM RECORD KEY
ATID     DC    CL8'  '       TITLE ID
ARID     DC    CL8'  '       REPORT ID
APDATE   DC    CL6'000000'   REPORT DATE
APPAGES  DC    PL4'0'        PAGES
         SPACE
* CURRENT INDEX FILE RECORD
INDEXREC DS    0CL100
XIXKEY   DS    0CL31          VSAM RECORD KEY
XTYPE    DC    CL1'T'        TYPE OF INDEX RECORD
XBANK    DC    PL2'0'        BANK
XBRANCH  DC    PL2'0'        BRANCH
XTID     DC    CL8'  '       TITLE ID
XRID     DC    CL8'  '       REPORT ID
XIDATE   DC    CL6'000000'   REPORT DATE
XFILLER  DC    CL4' '        FILLER OF VSAM KEY
XPTTIT   DC    CL50' '       REPORT TITLE NAME
XPPAGES  DC    PL4'0'        PAGES
XPLINES  DC    PL5'0'        LINES
XFILL1   DC    CL10' '       AVALIABLE SPACES
         EJECT
*----------------------------------------------------------------------
* DDA STATEMENT TITLE RECORD DATA  ------------------------------------
*----------------------------------------------------------------------
         SPACE
DDATITLE DS    0CL170
         DC    CL1'T'                 RECORD TYPE
         DC    CL8'DDASTMTS'          REPORT ID
         DC    CL8' '                 SECONDARY REPORT ID
         DC    CL3'DDS'               APPLICATION CODE
         DC    PL2'0'                 NUMBER OF CYCLES
         DC    PL2'0'                 KEEP DAYS
         DC    CL1'N'                 TYPE CYCLE
         DC    CL1'Y'                 EXTRACT
         DC    PL2'0'                 FORCE BANK NUMBER
         DC    10PL2'0'               EXTRACT BANK NUMBERS
         DC    CL50'DDA STATEMENTS'   REPORT TITLE NAME
         DC    0CL50'DDA STATEMENTS'    TITLE DATA
* REDEFINE OF TITLE DATA FIELD USED BY DDA STATEMENTS
* TO FIND THE BANK/BRANCH/ACCOUNT ON BOTH SIDES OF STATEMENTS
         DC    PL2'10'                BANK/BRANCH1 LINE
         DC    PL2'53'                BANK/BRANCH1 POSITION
         DC    PL2'6'                 BANK/BRANCH1 LENGTH
         DC    PL2'12'                ACCOUNT1 LINE
         DC    PL2'51'                ACCOUNT1 POSITION
         DC    PL2'13'                ACCOUNT1 LENGTH
         DC    PL2'10'                BANK/BRANCH2 LINE
         DC    PL2'120'               BANK/BRANCH2 POSITION
         DC    PL2'6'                 BANK/BRANCH2 LENGTH
         DC    PL2'12'                ACCOUNT2 LINE
         DC    PL2'118'               ACCOUNT2 POSITION
         DC    PL2'13'                ACCOUNT2 LENGTH
         DC    CL26' '                AVAILABLE FILLER
*  END OF REDEFINED DDA STATEMENT DATA
*  START OF NORMAL DATA
         DC    PL2'0'                 TITLE LINE 1
         DC    PL2'0'                 TITLE POSITION 1
         DC    PL2'0'                 TITLE LENGTH 1
         DC    CL50' '                REPORT TITLE NAME 2
         DC    PL2'0'                 TITLE LINE 2
         DC    PL2'0'                 TITLE POSITION 2
         DC    PL2'0'                 TITLE LENGTH 2
         DC    CL50' '                REPORT TITLE NAME 3
         DC    PL2'0'                 TITLE LINE 3
         DC    PL2'0'                 TITLE POSITION 3
         DC    PL2'0'                 TITLE LENGTH 3
         DC    PL2'8'                 DATE LINE
         DC    PL2'57'                DATE POSITION
         DC    CL1'N'                 DATE TYPE
         DC    PL2'10'                KEY 1 LINE
         DC    PL2'53'                KEY 1 POSITION
         DC    PL2'6'                 KEY 1 LENGTH
         DC    PL2'12'                KEY 2 LINE
         DC    PL2'51'                KEY 2 POSITION
         DC    PL2'13'                KEY 2 LENGTH
         DC    PL2'0'                 KEY 3 LINE
         DC    PL2'0'                 KEY 3 POSITION
         DC    PL2'0'                 KEY 3 LENGTH
         DC    PL2'10'                BANK  LINE
         DC    PL2'53'                BANK  POSITION
         DC    PL2'3'                 BANK  LENGTH
         DC    PL2'10'                BRANCH LINE
         DC    PL2'56'                BRANCH POSITION
         DC    PL2'3'                 BRANCH LENGTH
         DC    CL9' '                 AVAILABLE FILLER
       EJECT
*----------------------------------------------------------------------
* REPORT OUTPUT DETAIL RECORD LAYOUT ----------------------------------
*----------------------------------------------------------------------
         SPACE
DRECORD  DS    0F
DRECLNG  DS    H
DRECLNG1 DS    H
DRECID   DS    CL8
DDATE    DS    CL6
DPAGE    DS    PL4
DNLINES  DS    PL2
DLINE    DS    88CL133
       EJECT
*----------------------------------------------------------------------
* TITLE TABLE RECORD LAYOUT -------------------------------------------
*----------------------------------------------------------------------
         SPACE
* REPORT ID RECORD LAYOUT
RDSECT   DSECT
REPT1    EQU   *
RPTYPE   DS    CL1           TYPE RECORD
RTID     DS    CL8           TITLE ID
RRID     DS    CL8           REPORT ID
RPTAPL   DS    CL3           APPLICATION
RPTCYL   DS    PL2           CYCLES
RPTKEEP  DS    PL2           KEEP DAYS
RPTCTYP  DS    CL1           CYCLE TYPE
RPTEXT   DS    CL1           EXTRACT STATUS
RPTFRC   DS    PL2           FORCED BANK NUMBER
RPTBNK   DS    10PL2         BANK NUMBERS TO EXTRACT
RPTTIT   DS    CL50          REPORT TITLE
RPTTITD1 DS    0CL50         REPORT TITLE DATA 1
RPKBL1   DS    PL2           DDA STMT BANK/BRANCH1 LINE NUMBER
RPKBP1   DS    PL2           DDA STMT BANK/BRANCH1 POSIITION
RPKBK1   DS    PL2           DDA STMT BANK/BRANCH1 LENGTH
RPKAL1   DS    PL2           DDA STMT ACCOUNT1 LINE NUMBER
RPKAP1   DS    PL2           DDA STMT ACCOUNT1 POSIITION
RPKAK1   DS    PL2           DDA STMT ACCOUNT1 LENGTH
RPKBL2   DS    PL2           DDA STMT BANK/BRANCH2 LINE NUMBER
RPKBP2   DS    PL2           DDA STMT BANK/BRANCH2 POSIITION
RPKBK2   DS    PL2           DDA STMT BANK/BRANCH2 LENGTH
RPKAL2   DS    PL2           DDA STMT ACCOUNT2 LINE NUMBER
RPKAP2   DS    PL2           DDA STMT ACCOUNT2 POSIITION
RPKAK2   DS    PL2           DDA STMT ACCOUNT2 LENGTH
         DS    CL26
RPTTL1   DS    PL2           REPORT TITLE LINE NUMBER 1
RPTRP1   DS    PL2           REPORT TITLE POSITION 1
RPTRL1   DS    PL2           REPORT TITLE LENGTH 1
RPTTITD2 DS    CL50          REPORT TITLE DATA 2
RPTTL2   DS    PL2           REPORT TITLE LINE NUMBER 2
RPTRP2   DS    PL2           REPORT TITLE POSITION 2
RPTRL2   DS    PL2           REPORT TITLE LENGTH 2
RPTTITD3 DS    CL50          REPORT TITLE DATA 3
RPTTL3   DS    PL2           REPORT TITLE LINE NUMBER 3
RPTRP3   DS    PL2           REPORT TITLE POSITION 3
RPTRL3   DS    PL2           REPORT TITLE LENGTH 3
RPDTL    DS    PL2           REPORT DATE LINE NUMBER
RPDTP    DS    PL2           REPORT DATE POSIITION
RPDTT    DS    CL1           REPORT DATE TYPE
RPKTL1   DS    PL2           REPORT KEY 1 LINE NUMBER
RPKRP1   DS    PL2           REPORT KEY 1 POSIITION
RPKRL1   DS    PL2           REPORT KEY 1 LENGTH
RPKTL2   DS    PL2           REPORT KEY 2 LINE NUMBER
RPKRP2   DS    PL2           REPORT KEY 2 POSIITION
RPKRL2   DS    PL2           REPORT KEY 2 LENGTH
RPKTL3   DS    PL2           REPORT KEY 3 LINE NUMBER
RPKRP3   DS    PL2           REPORT KEY 3 POSIITION
RPKRL3   DS    PL2           REPORT KEY 3 LENGTH
RPBTL    DS    PL2           BANK NUMBER LINE NUMBER
RPBRP    DS    PL2           BANK NUMBER POSIITION
RPBRL    DS    PL2           BANK NUMBER LENGTH
RPRTL    DS    PL2           BRANCH NUMBER LINE NUMBER
RPRRP    DS    PL2           BRANCH NUMBER POSIITION
RPRRL    DS    PL2           BRANCH NUMBER LENGTH
         DS    CL9
REPT2    EQU   *-REPT1
         SPACE
* REPORT RECORD LAYOUT
RTDSECT  DSECT
REPTR1   EQU   *
TPTYPE   DS    CL1           TYPE RECORD
TTID     DS    CL8           TITLE ID
TRID     DS    CL8           REPORT ID
TPTAPL   DS    CL3           APPLICATION ID
TPTEXT   DS    CL1           SECONDARY EXTRACT STATUS
TPTBNK   DS    10PL2         BANKS NUMBERS TO EXTRACT
TPTTIT   DS    CL50          REPORT TITLE
TPTTITD1 DS    CL30          REPORT TITLE DATA 1
TPTTL1   DS    PL2           REPORT TITLE LINE NUMBER 1
TPTRP1   DS    PL2           REPORT TITLE POSITION 1
TPTRL1   DS    PL2           REPORT TITLE LENGTH 1
TPTTITD2 DS    CL30          REPORT TITLE DATA 2
TPTTL2   DS    PL2           REPORT TITLE LINE NUMBER 2
TPTRP2   DS    PL2           REPORT TITLE POSITION 2
TPTRL2   DS    PL2           REPORT TITLE LENGTH 2
TPTTITD3 DS    CL30          REPORT TITLE DATA 3
TPTTL3   DS    PL2           REPORT TITLE LINE NUMBER 3
TPTRP3   DS    PL2           REPORT TITLE POSITION 3
TPTRL3   DS    PL2           REPORT TITLE LENGTH 3
TPKTL1   DS    PL2           REPORT KEY 1 LINE NUMBER
TPKRP1   DS    PL2           REPORT KEY 1 POSIITION
TPKRL1   DS    PL2           REPORT KEY 1 LENGTH
TPKTL2   DS    PL2           REPORT KEY 2 LINE NUMBER
TPKRP2   DS    PL2           REPORT KEY 2 POSIITION
TPKRL2   DS    PL2           REPORT KEY 2 LENGTH
TPKTL3   DS    PL2           REPORT KEY 3 LINE NUMBER
TPKRP3   DS    PL2           REPORT KEY 3 POSIITION
TPKRL3   DS    PL2           REPORT KEY 3 LENGTH
         DS    CL24
REPT3    EQU   *-REPTR1
         EJECT
         END
