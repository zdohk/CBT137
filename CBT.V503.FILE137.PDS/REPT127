000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.        REPT127.
000300
000400 AUTHOR.            BILLY FENWICK
000500*
000600*  THIS PROGRAMS READS THE PURGED RECORDS FILE AND DELETES
000700*  ALL OF THE MATCHING 'K' INDEX RECORDS
000800*  FROM THE ONLINE INDEX FILE.
000900*
000600****************************************
000610*     (C) COPYRIGHT BILLY FENWICK
000620****************************************
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 INPUT-OUTPUT SECTION.
001300 FILE-CONTROL.
001400     SELECT REPORT-INDEX-FILE   ASSIGN TO DA-IDXIN
001500                         ORGANIZATION IS INDEXED
001600                         ACCESS IS DYNAMIC
001700                         FILE STATUS IS IDX-STATUS
001800                         RECORD KEY IS K-KEY.
001900     SELECT PURGE-RECORDS    ASSIGN TO UT-S-PURGEDI.
001910     SELECT REPORT-LIST-FILE   ASSIGN TO UT-S-REPTLST.
002000
002100 DATA DIVISION.
002200 FILE SECTION.
002300
002400 FD  PURGE-RECORDS
002500     RECORDING MODE F
002600     BLOCK CONTAINS 0 RECORDS
002700     LABEL RECORDS ARE STANDARD
002800     RECORD CONTAINS 22.
002900 01  PURGE-RECORD.
003000     03 PI-REPORT-ID           PIC X(8).
003100     03 PI-REPORT-SID          PIC X(8).
003200     03 PI-REPORT-DATE         PIC 9(6).
003300
003400 FD  REPORT-INDEX-FILE
003500     RECORD CONTAINS 57.
003600     COPY REPTB06 REPLACING ==:Z:== BY ==K==.
003700
003701 FD  REPORT-LIST-FILE
003702     RECORDING MODE IS F
003703     RECORD CONTAINS 132.
003704 01  ADD-PRINT-RECORD.
003705     03 PRT-ONE                 PIC X(50).
003706     03 PRT-XYZ                 PIC X(82).
003707     03 X-FIL REDEFINES PRT-XYZ.
003708        05 PRT-ID                  PIC X(8).
003709        05 FILLER                  PIC X(1).
003710        05 PRT-SID                 PIC X(8).
003711        05 FILLER                  PIC X(1).
003712        05 PRT-DATE                PIC 9(6).
003713        05 FILLER                  PIC X(1).
003714        05 PRT-BANK                PIC 9(3).
003715        05 FILLER                  PIC X(1).
003716        05 PRT-BRANCH              PIC 9(3).
003717        05 FILLER                  PIC X(1).
003718        05 PRT-SEQ                 PIC 9(7).
003719        05 FILLER                  PIC X(42).
003720     03 X-FILL REDEFINES PRT-XYZ.
003721        05 PRT-NUMBER              PIC ZZZ,Z99.
003722        05 FILLER                  PIC X(75).
003730
003800 WORKING-STORAGE SECTION.
003900 77  KK-COUNT      PIC S9(7) COMP-3  VALUE ZERO.
004000 77  SUB           PIC S9(4) COMP    VALUE ZERO.
004100 77  II-SEQ        PIC S9(7)         VALUE ZERO.
004200 77  IDX-STATUS    PIC X(2)          VALUE 'ZZ'.
004300
004400 EJECT
004410 LINKAGE SECTION.
004420 01  LINK-WORK-AREA.
004430     03  PARM-LENGTH   PIC S9(4) COMP.
004440     03  JOB-PARM      PIC X(5).
004450        88 DEBUG-ON  VALUE 'DEBUG'.
004460        88 REPORT-ON VALUE 'REPRT'.
004470
004480 EJECT
004490 PROCEDURE DIVISION USING LINK-WORK-AREA.
004600 0000-MAIN-ROUTINE.
004700
004710     OPEN OUTPUT REPORT-LIST-FILE.
004800     OPEN INPUT PURGE-RECORDS
004900          I-O REPORT-INDEX-FILE.
004910     IF REPORT-ON
004920        MOVE ALL '-' TO ADD-PRINT-RECORD
004930        WRITE ADD-PRINT-RECORD
004940     END-IF.
005000
005100 2000-READ-PURGE-RECORD.
005200     READ PURGE-RECORDS AT END GO TO 9000-CLOSE-FILES.
005210     IF DEBUG-ON
005220        DISPLAY '2000-READ-PURGE-RECORD'
005230        DISPLAY '   PI-REPORT-ID=' PI-REPORT-ID
005240        DISPLAY '  PI-REPORT-SID=' PI-REPORT-SID
005250        DISPLAY ' PI-REPORT-DATE=' PI-REPORT-DATE.
005300
005400 3100-PURGE-INDEX-RECORDS.
005500     MOVE LOW-VALUES TO K-INDEX-RECORD.
005510     MOVE PI-REPORT-ID TO K-REPORT-ID.
005520     MOVE PI-REPORT-SID TO K-REPORT-SID.
005600     MOVE PI-REPORT-DATE TO K-REPORT-DATE.
005900     MOVE 'K' TO K-TYPE.
006000     START REPORT-INDEX-FILE KEY IS GREATER THAN K-KEY
006100        INVALID KEY GO TO 9300-BAD-START.
006200     IF IDX-STATUS NOT EQUAL ZERO
006300        GO TO 9300-BAD-START.
006400
006500 3150-READ-INDEX-FILE.
006600     READ REPORT-INDEX-FILE NEXT RECORD AT END
006700        GO TO 2000-READ-PURGE-RECORD.
006710     IF DEBUG-ON
006720        DISPLAY '3150-READ-INDEX-FILE'
006730        DISPLAY '  K-REPORT-ID= ' K-REPORT-ID
006740        DISPLAY '  K-REPORT-SID= ' K-REPORT-SID
006750        DISPLAY '  K-REPORT-DATE= ' K-REPORT-DATE
006760        DISPLAY '  K-BANK= ' K-BANK
006770        DISPLAY '  K-BRANCH= ' K-BRANCH
006780        DISPLAY '  K-SEQ= ' K-SEQ
006790     END-IF.
006800     IF (K-REPORT-ID EQUAL PI-REPORT-ID) AND
006810        (K-REPORT-SID EQUAL PI-REPORT-SID) AND
006900        (K-REPORT-DATE EQUAL PI-REPORT-DATE )
007000        GO TO 3200-DELETE-RECORD.
007100     GO TO 2000-READ-PURGE-RECORD.
007200
007300 3200-DELETE-RECORD.
007400     MOVE K-SEQ TO II-SEQ.
007410     IF DEBUG-ON
007500        DISPLAY ' TYPE -K- INDEX RECORD = '
007600             K-REPORT-DATE ' ' K-REPORT-ID ' '
007700             K-REPORT-SID ' ' II-SEQ.
007800     ADD 1 TO KK-COUNT.
007810     IF REPORT-ON
007820        MOVE SPACES TO ADD-PRINT-RECORD
007830        MOVE '  RECORD DELETED ---' TO PRT-ONE
007840        MOVE K-REPORT-ID TO PRT-ID
007841        MOVE K-REPORT-SID TO PRT-SID
007850        MOVE K-REPORT-DATE TO PRT-DATE
007860        MOVE K-BANK TO PRT-BANK
007861        MOVE K-BRANCH TO PRT-BRANCH
007862        MOVE K-SEQ TO PRT-SEQ
007870        WRITE ADD-PRINT-RECORD
007880     END-IF.
007900     DELETE REPORT-INDEX-FILE RECORD   INVALID KEY
008000        GO TO 9920-INDEX-FILE.
008100     IF IDX-STATUS NOT EQUAL ZERO
008200        GO TO 9920-INDEX-FILE.
008300     GO TO 3150-READ-INDEX-FILE.
008400
008500 9000-CLOSE-FILES.
008510     IF REPORT-ON
008520        MOVE ALL '-' TO ADD-PRINT-RECORD
008530        WRITE ADD-PRINT-RECORD
008540        MOVE SPACES TO ADD-PRINT-RECORD
008550        MOVE ' TOTAL INDEX TYPE "K" RECORDS DELETED -' TO PRT-ONE
008560        MOVE KK-COUNT TO PRT-NUMBER
008570        WRITE ADD-PRINT-RECORD
008580        MOVE ALL '-' TO ADD-PRINT-RECORD
008590        WRITE ADD-PRINT-RECORD
008591     END-IF.
008900     CLOSE PURGE-RECORDS
009000           REPORT-INDEX-FILE
009010           REPORT-LIST-FILE.
009100     GO TO 9999-END-OF-PROGRAM.
009200
009300 9300-BAD-START.
009400     DISPLAY '*** INVALID START BROWSE ON FILE IDXIN**'.
009500     MOVE 16 TO RETURN-CODE
009600     GO TO 9000-CLOSE-FILES.
009700
009800 9920-INDEX-FILE.
009900     DISPLAY '*** DELETE ERROR ON INDEX-FILE ***'.
010000     MOVE 14 TO RETURN-CODE
010100     GO TO 9000-CLOSE-FILES.
010200
010300 9999-END-OF-PROGRAM.
010400     STOP RUN.
