/* REXX EXEC TO list the "T" History records  */

"EXECIO * DISKR INPUT (FINI STEM inrec."
  header1 = 'Type Bank Branch Report-id Report-sid  Date'
  header1 = header1 || '    Pages     Lines DD-Name Report-Name'
  rec_count = 0
  k_count = 0
  say '------------T RECORDS------------'
  say header1
do i=1 to inrec.0
   rec_count = rec_count + 1
   ttype = (substr(inrec.i,1,1))
   tbankp = (substr(inrec.i,2,2))
   tbank = unpack(tbankp)
   tbranchp = (substr(inrec.i,4,2))
   tbranch = unpack(tbranchp)
   trid  = (substr(inrec.i,6,8))
   trids  = (substr(inrec.i,14,8))
   tdate  = (substr(inrec.i,22,6))
   report_name = (substr(inrec.i,32,50))
   tnumpagesp = (substr(inrec.i,82,4))
   tnumpages = unpack(tnumpagesp)
   tnumlinesp = (substr(inrec.i,86,5))
   tnumlines = unpack(tnumlinesp)
   tfilename = (substr(inrec.i,91,6))
   line1 = ' ' || ttype || '   ' || tbank || '   '|| tbranch || '   '
   line1 = line1 || trid || '   ' || trids ' ' || tdate || ' '
   line1 = line1 || tnumpages || ' ' || tnumlines
   line1 = line1 || '  ' || tfilename || ' ' || report_name
   say line1
   k_count = k_count + tnumpages
end
 say '------------TOTAL T Record Count------'
 say '  Total T Records=' rec_count
 say '------------TOTAL K Record Count------'
 say '  Total K Records=' k_count
exit
unpack: procedure
    parse arg pack
    /* Convert packed data to hex and split */
    char = c2x(pack)
    numb = left( char, length(char)-1 )
    sign = right( char, 1 )
    /* Check sign and numeric sections */
    if  verify(sign, "ABCDEF" ) > 0 then ,
        return ""
    if  verify( numb, "0123456789" ) > 0 then ,
        return ""

    /* Check negative sign */
    if  pos(sign, "BD" ) > 0 then,
        return -numb
    else ,
    return numb
