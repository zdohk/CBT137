000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.        REPT025.
000300
000400 AUTHOR.            BILLY FENWICK
000500
000600*   READS THE LIST OF TITLE RECORDS.  WRITES OUT A LIST
000700*   OF THE REPORTS TO BE DROPPED.
000600****************************************
000610*     (C) COPYRIGHT BILLY FENWICK
000620****************************************
000800
000900 ENVIRONMENT DIVISION.
001000 CONFIGURATION SECTION.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300     SELECT REPORT-TITLE-FILE  ASSIGN TO DA-TITLE
001400                         ORGANIZATION IS INDEXED
001500                         ACCESS IS RANDOM
001600                         FILE STATUS IS TIT-STATUS
001700                         RECORD KEY IS T-KEY.
001800     SELECT TITLES-PURGED-FILE  ASSIGN TO UT-S-PURGED.
001900     SELECT INDEX-PURGED-FILE   ASSIGN TO UT-S-PURGEDI.
002000     SELECT T-RECORDS-FILE      ASSIGN TO UT-S-TRECS.
002100
002200 DATA DIVISION.
002300 FILE SECTION.
002400
002500 FD  REPORT-TITLE-FILE
002600     RECORD CONTAINS 240 TO 310.
002700 01  TITLE-RECORD.
002800     COPY REPTB03 REPLACING ==:Z:== BY ==T==.
002900
003000 01  REPORT-RECORD.
003100     COPY REPTB04 REPLACING ==:Z:== BY ==R==.
003200
003300 FD  T-RECORDS-FILE
003400     BLOCK CONTAINS 0 RECORDS
003500     RECORDING MODE IS F
003600     RECORD CONTAINS 100.
003700 01  X-RECORD.
003800     COPY REPTB01 REPLACING ==:Z:== BY ==X==.
003900
004000 FD  TITLES-PURGED-FILE
004100     LABEL RECORDS ARE STANDARD
004200     RECORDING MODE IS F
004300     BLOCK CONTAINS 0 RECORDS
004400     RECORD CONTAINS 14.
004500 01  PURGE-RECORD.
004600     03 P-REPORT-ID            PIC X(8).
004700     03 P-REPORT-DATE          PIC 9(6).
004800
004900 FD  INDEX-PURGED-FILE
005000     LABEL RECORDS ARE STANDARD
005100     RECORDING MODE IS F
005200     BLOCK CONTAINS 0 RECORDS
005300     RECORD CONTAINS 22.
005400 01  PURGE-I-RECORD.
005500     03 O-REPORT-ID            PIC X(8).
005600     03 O-REPORT-SID           PIC X(8).
005700     03 O-REPORT-DATE          PIC 9(6).
005800
005900 WORKING-STORAGE SECTION.
006000 77  T-COUNT       PIC 9(5)  COMP-3  VALUE ZERO.
006100 77  TIT-STATUS    PIC X(2)          VALUE 'ZZ'.
006200 77  WRK-COUNT     PIC 9(5)  COMP-3  VALUE ZERO.
006300 77  GET-OUT-1     PIC X(1)          VALUE 'N'.
006400 88  GET-OUT               VALUE 'Y'.
006500
006600 01  WORK-ARRAY.
006700     03 WRK-REPORT-ARRAY  PIC X(800).
006710     03 FILLER REDEFINES WRK-REPORT-ARRAY.
006720        05 WRK-REPORT-SID  PIC X(8)  OCCURS 100 TIMES.
006800
006900 01  WORK-AREA.
007000     03  OLD-REPORT-ID      PIC  X(8) VALUE SPACES.
007100     03  OLD-REPORT-SID     PIC  X(8) VALUE SPACES.
007200     03  OLD-REPORT-DATE    PIC  9(6) VALUE ZEROS.
007300     03  SYSTEM-DAYS        PIC  S9(7) VALUE ZEROS COMP-3.
007400     03  TOTAL-DAYS         PIC  S9(7) VALUE ZEROS COMP-3.
007500     03  MONTH-DAYS         PIC  S9(7) VALUE ZEROS COMP-3.
007600     03  YEAR-CHECK         PIC   9(2) VALUE ZEROS.
007700     03  I                  PIC   9(2) VALUE ZEROS.
007800     03  DAYS-IN-MONTH.
007900         05  FILLER PIC 9(2) VALUE 31.
008000         05  FILLER PIC 9(2) VALUE 28.
008100         05  FILLER PIC 9(2) VALUE 31.
008200         05  FILLER PIC 9(2) VALUE 30.
008300         05  FILLER PIC 9(2) VALUE 31.
008400         05  FILLER PIC 9(2) VALUE 30.
008500         05  FILLER PIC 9(2) VALUE 31.
008600         05  FILLER PIC 9(2) VALUE 31.
008700         05  FILLER PIC 9(2) VALUE 30.
008800         05  FILLER PIC 9(2) VALUE 31.
008900         05  FILLER PIC 9(2) VALUE 30.
009000         05  FILLER PIC 9(2) VALUE 31.
009100     03  DAYS REDEFINES DAYS-IN-MONTH PIC 9(2) OCCURS 12 TIMES.
009200     03  DATE-G PIC 9(8).
009300     03  FILLER REDEFINES DATE-G.
009400         05  DATE-YY  PIC 9(4).
009500         05  DATE-MM  PIC 9(2).
009600         05  DATE-DD  PIC 9(2).
009700     03  SYSTEM-DATE     PIC X(8).
009800     03  FILLER REDEFINES SYSTEM-DATE.
009900         05  SS-YY  PIC 9(4).
010000         05  SS-MM  PIC 9(2).
010100         05  SS-DD  PIC 9(2).
010200
010300 EJECT
010400 LINKAGE SECTION.
010500 01  LINK-WORK-AREA.
010600     03  PARM-LENGTH   PIC S9(4) COMP.
010700     03  JOB-PARM      PIC X(5).
010800        88 DEBUG-ON  VALUE 'DEBUG'.
010900        88 REPORT-ON VALUE 'REPRT'.
011000
011100 EJECT
011200 PROCEDURE DIVISION USING LINK-WORK-AREA.
011300
011400 0000-OPEN-FILES.
011500     OPEN INPUT REPORT-TITLE-FILE
011600                T-RECORDS-FILE
011700          OUTPUT TITLES-PURGED-FILE
011800                 INDEX-PURGED-FILE.
011900     MOVE FUNCTION CURRENT-DATE (1:8) TO SYSTEM-DATE.
012000     IF DEBUG-ON
012100        DISPLAY '  SYSTEM DATE=' SYSTEM-DATE.
012200     MOVE SS-YY TO DATE-YY.
012300     MOVE SS-MM TO DATE-MM.
012400     MOVE SS-DD TO DATE-DD.
012500     PERFORM 1700-CONVERT-DATE.
012600     MOVE TOTAL-DAYS TO SYSTEM-DAYS.
012700     MOVE SPACES TO WORK-ARRAY.
012800
012900 1000-T-INDEX-FILE.
013000     READ T-RECORDS-FILE AT END
013100        GO TO 9000-CLOSE-FILES.
013200     IF DEBUG-ON
013300       DISPLAY '1000-T-INDEX-FILE'
013400       DISPLAY '  X-REPORT-ID=' X-REPORT-ID
013500       DISPLAY '  X-REPORT-SID=' X-REPORT-SID
013600       DISPLAY '  X-REPORT-DATE=' X-REPORT-DATE.
013700     IF X-TYPE EQUAL 'T'
013800        NEXT SENTENCE  ELSE
013900        GO TO 9306-BAD-T-RECORDS.
014000     IF OLD-REPORT-ID NOT EQUAL X-REPORT-ID
014010        MOVE LOW-VALUES TO WRK-REPORT-ARRAY
014100        PERFORM 1100-READ-T-INDEX-RECORDS
014200        PERFORM 1200-CHECK-REPORT-ID
014300        GO TO 1000-T-INDEX-FILE.
014400     IF OLD-REPORT-DATE NOT EQUAL X-REPORT-DATE
014500        PERFORM 1500-CHECK-FOR-PURGE
014600        PERFORM 1200-CHECK-REPORT-ID.
014700     IF OLD-REPORT-SID NOT EQUAL X-REPORT-SID
014800        PERFORM 1200-CHECK-REPORT-ID
014900        GO TO 1000-T-INDEX-FILE.
015000     GO TO 1000-T-INDEX-FILE.
015100
015200 1100-READ-T-INDEX-RECORDS.
015300     MOVE LOW-VALUES TO TITLE-RECORD.
015400     MOVE 'T' TO T-TYPE.
015500     MOVE 1 TO T-COUNT.
015600     MOVE X-REPORT-ID TO T-REPORT-ID, OLD-REPORT-ID.
015700     MOVE X-REPORT-SID TO OLD-REPORT-SID.
015800     MOVE X-REPORT-DATE TO OLD-REPORT-DATE.
015900     MOVE SPACES TO T-REPORT-SID.
016000     READ REPORT-TITLE-FILE KEY IS T-KEY
016100        INVALID KEY
016200              PERFORM 2000-WRITE-RECORD
016300              GO TO 9300-BAD-START.
016400     IF DEBUG-ON
016500       DISPLAY '1100-READ-T-INDEX-FILE'
016600       DISPLAY '  T-REPORT-ID=' T-REPORT-ID
016700       DISPLAY '  T-REPORT-SID=' T-REPORT-SID
016800       DISPLAY '  T-CYLES=' T-CYCLES
016900       DISPLAY '  T-CYCLE-TYPE=' T-CYCLE-TYPE
017000       DISPLAY '  T-COUNT=' T-COUNT.
017100     IF T-CYCLE-TYPE EQUAL 'C' AND
017200        T-COUNT GREATER THAN T-CYCLES
017300        PERFORM 2000-WRITE-RECORD.
017400     IF T-CYCLE-TYPE EQUAL 'D'
017500        PERFORM 1600-CHECK-DAYS.
017600
017700 1200-CHECK-REPORT-ID.
017800     IF DEBUG-ON
017900        DISPLAY '1200-CHECK-REPORT-ID'
018000        DISPLAY '  X-REPORT-ID=' X-REPORT-ID
018100        DISPLAY '  X-REPORT-SID=' X-REPORT-SID
018200        DISPLAY '  X-REPORT-DATE=' X-REPORT-DATE.
018300     MOVE 'N' TO GET-OUT-1.
018400     PERFORM 1210-ADD-TO-ARRAY THRU 1215-ADD-EXIT
018500        VARYING I FROM 1 BY 1 UNTIL GET-OUT.
018600
018700 1210-ADD-TO-ARRAY.
018800     IF DEBUG-ON
018900        DISPLAY '1210-ADD-TO-ARRAY'
019000        DISPLAY '              I=' I
019010        DISPLAY '   X-REPORT-SID=' X-REPORT-SID
019020        DISPLAY ' WRK-REPORT-SID=' WRK-REPORT-SID (I).
019300     IF WRK-REPORT-SID (I) EQUAL X-REPORT-SID
019400        MOVE 'Y' TO GET-OUT-1
019410        GO TO 1215-ADD-EXIT.
019500     IF WRK-REPORT-SID (I) EQUAL LOW-VALUES
019600        MOVE X-REPORT-SID TO WRK-REPORT-SID (I)
019700        MOVE 'Y' TO GET-OUT-1
019701        GO TO 1215-ADD-EXIT.
019705     IF WRK-REPORT-SID (I) EQUAL LOW-VALUES
019706        MOVE 'Y' TO GET-OUT-1
019707        GO TO 1215-ADD-EXIT.
019760
019770 1215-ADD-EXIT.  EXIT.
019800
019900 1500-CHECK-FOR-PURGE.
020000     MOVE X-REPORT-DATE TO OLD-REPORT-DATE.
020100     ADD 1 TO T-COUNT.
020200     IF DEBUG-ON
020300        DISPLAY '1500-CHECK-FOR-PURGE'
020400        DISPLAY ' T-COUNT=' T-COUNT
020500        DISPLAY ' T-CYCLE-TYPE=' T-CYCLE-TYPE
020600        DISPLAY ' T-CYCLES=' T-CYCLES.
020700     IF T-CYCLE-TYPE EQUAL 'C' AND
020800        T-COUNT GREATER THAN T-CYCLES
020900        PERFORM 2000-WRITE-RECORD.
021000     IF T-CYCLE-TYPE EQUAL 'D'
021100        PERFORM 1600-CHECK-DAYS.
021200
021300 1600-CHECK-DAYS.
021400     MOVE X-REPORT-DATE TO DATE-G.
021500     PERFORM 1700-CONVERT-DATE.
021600     SUBTRACT TOTAL-DAYS FROM SYSTEM-DAYS GIVING TOTAL-DAYS.
021700     IF DEBUG-ON
021800       DISPLAY '1600-CHECK-DAYS'
021900       DISPLAY '  TOTAL-DAYS=' TOTAL-DAYS
022000       DISPLAY ' T-KEEP-DAYS=' T-KEEP-DAYS.
022100     IF TOTAL-DAYS GREATER THAN T-KEEP-DAYS OR
022200        TOTAL-DAYS EQUAL T-KEEP-DAYS
022300        PERFORM 2000-WRITE-RECORD.
022400
022500
022600 1700-CONVERT-DATE.
022700     IF DEBUG-ON
022800       DISPLAY '1700-CONVERT-DATE'.
022900     MOVE 0 TO TOTAL-DAYS, MONTH-DAYS.
023000     COMPUTE TOTAL-DAYS = (DATE-YY - 2015) * 365.
023100     PERFORM 1710-GET-MONTH
023200        VARYING I FROM 1 BY 1 UNTIL I = DATE-MM.
023300     ADD MONTH-DAYS, DATE-DD TO TOTAL-DAYS.
023400     COMPUTE YEAR-CHECK = (DATE-YY / 4) * 4.
023500     IF YEAR-CHECK = DATE-YY AND DATE-MM = 02 AND DATE-DD = 29
023600        ADD  1 TO TOTAL-DAYS.
023700     IF YEAR-CHECK = DATE-YY AND DATE-MM GREATER THAN 02
023800        ADD  1 TO TOTAL-DAYS.
023900
024000 1710-GET-MONTH.
024100     IF I = DATE-MM
024200       NEXT SENTENCE ELSE
024300         ADD DAYS (I) TO MONTH-DAYS.
024400
024500 2000-WRITE-RECORD.
024600     IF DEBUG-ON
024700          DISPLAY '2000-WRITE-RECORD'
024800          DISPLAY '   X-REPORT-ID=' X-REPORT-ID
024900          DISPLAY '   X-REPORT-SID=' X-REPORT-SID
025000          DISPLAY '   X-REPORT-DATE=' X-REPORT-DATE.
025100     MOVE X-REPORT-ID TO P-REPORT-ID.
025200     MOVE X-REPORT-DATE TO P-REPORT-DATE.
025300     WRITE PURGE-RECORD  INVALID KEY
025400        GO TO 9920-WRITE-ERROR.
025500     MOVE 'N' TO GET-OUT-1.
025600     PERFORM 2100-WRITE-SID-RECORD THRU
025610             2110-WRITE-EXIT
025700        VARYING I FROM 1 BY 1 UNTIL GET-OUT.
025800
025900 2100-WRITE-SID-RECORD.
026000     IF DEBUG-ON
026100          DISPLAY '2100-WRITE-SID-RECORD'
026200          DISPLAY '     X-REPORT-ID=' X-REPORT-ID
026300          DISPLAY '  WRK-REPORT-SID=' WRK-REPORT-SID (I)
026400          DISPLAY '   X-REPORT-DATE=' X-REPORT-DATE.
026500     IF WRK-REPORT-SID (I) EQUAL LOW-VALUES
026600        MOVE 'Y' TO GET-OUT-1
026610        GO TO 2110-WRITE-EXIT.
026700     MOVE X-REPORT-ID TO O-REPORT-ID.
026800     MOVE WRK-REPORT-SID (I) TO O-REPORT-SID.
026900     MOVE X-REPORT-DATE TO O-REPORT-DATE.
027000     WRITE PURGE-I-RECORD  INVALID KEY
027100         GO TO 9920-WRITE-ERROR.
027101
027102 2110-WRITE-EXIT.   EXIT.
027103
027300 9000-CLOSE-FILES.
027400     CLOSE T-RECORDS-FILE
027500           TITLES-PURGED-FILE
027600           INDEX-PURGED-FILE
027700           REPORT-TITLE-FILE.
027800     GO TO 9999-END-OF-PROGRAM.
027900
028000 9300-BAD-START.
028100     DISPLAY '*-------------------------------*'.
028200     DISPLAY '*** INVALID READ ON FILE TITLES**'.
028300     DISPLAY T-KEY.
028400     DISPLAY '*-------------------------------*'.
028500     MOVE 4 TO RETURN-CODE.
028600     MOVE SPACES TO OLD-REPORT-ID.
028700     GO TO 1000-T-INDEX-FILE.
028800
028900 9306-BAD-T-RECORDS.
029000     DISPLAY '*** INVALID TYPE RECORDS ON FILE TITIN**'.
029100     MOVE 22 TO RETURN-CODE
029200     GO TO 9000-CLOSE-FILES.
029300
029400 9920-WRITE-ERROR.
029500     DISPLAY '*** WRITE ERROR ON TRECS  FILE ***'.
029600     MOVE 14 TO RETURN-CODE
029700     GO TO 9000-CLOSE-FILES.
029800
029900 9999-END-OF-PROGRAM.
030000     STOP RUN.
