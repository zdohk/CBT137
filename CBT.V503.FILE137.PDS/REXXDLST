/* REXX EXEC TO list the "D" Detail Records  */

"EXECIO * DISKR INPUT (FINI STEM inrec."
  header1 = 'Report-id Date        Seq  Number-of-lines'
  rec_count = 0
  say '------------Detail RECORDS------------'
  say header1
do i=1 to inrec.0
   rec_count = rec_count + 1
   drid  = (substr(inrec.i,1,8))
   ddate  = (substr(inrec.i,9,6))
   dseqp = (substr(inrec.i,15,4))
   dseq = unpack(dseqp)
   dlinesp = (substr(inrec.i,19,2))
   dlines = unpack(dlinesp)
   line1 = drid ||' ' || ddate || '   '
   line1 = line1 || dseq || '   ' || dlines
   say line1
end
 say '------------TOTAL Detail Record Count------'
 say '  Total Detail Records=' rec_count
exit
unpack: procedure
    parse arg pack
    /* Convert packed data to hex and split */
    char = c2x(pack)
    numb = left( char, length(char)-1 )
    sign = right( char, 1 )
    /* Check sign and numeric sections */
    if  verify(sign, "ABCDEF" ) > 0 then ,
        return ""
    if  verify( numb, "0123456789" ) > 0 then ,
        return ""

    /* Check negative sign */
    if  pos(sign, "BD" ) > 0 then,
        return -numb
    else ,
    return numb
