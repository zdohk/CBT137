S10      TITLE 'ONLINE SUBROUTINE TO SEARCH FOR A CHARACTER STRING'
*        PRINT NOGEN
*----------------------------------------------------------------------
* THIS IS A SUBROUTINE USED BY THE ONLINE PROGRAM CICSRP03 TO SEARCH
* THE DETAIL RECORD PASSED TO THIS ROUTINE FOR A CHARACTER STRING.
* THE LOCATION OF THE HIT WILL BE PASSED BACK AND AN INDICATION OF
* WETHER THERE WAS A HIT OR NOT.  ALSO THERE CAN BE A STARTING POINT
* WITHIN THE RECORD OF WHERE TO START THE SEARCH.  ALL CHARACTER
* STRINGS "MUST" BE IN QUOTES.   EITHER SINGLE(') OR DOUBLE(")
*----------------------------------------------------------------------
****************************************
*     (C) COPYRIGHT BILLY FENWICK
****************************************
* THIS PROGRAM USES THE FOLLOWING REGISTERS
*  R1   AVAILABLE
*  R2   AVAILABLE
*  R3   DSECT FOR COBOL PROGRAM
*  R4   DSECT OF DETAIL RECORD
*  R5   AVAILABLE
*  R6   AVAILABLE
*  R7   AVAILABLE
*  R8   AVAILABLE
*  R9   AVAILABLE
*  R10  AVAILABLE
*  R11  SAVE STARTING POINT OF DETAIL RECORD
*  R12  BASE
*  R13  BASE
*  R14  LINKAGE
*  R15  AVAILABLE
         SPACE
*----------------------------------------------------------------------
* LINKAGE SECTION -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
REPTS10  CSECT
         SAVE  (14,12)
         BALR   R12,0                   ESTABLISH BASE REGISTER
         USING  *,R12
         B     MAIN
         SPACE
*----------------------------------------------------------------------
* MAIN PROCESSING -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
         USING PARM1,R3
         USING PARM2,R4
MAIN     L     R3,0(R1)           LOAD ADDRESS WK-TOTAL-LINES
         L     R4,4(R1)           LOAD ADDRESS D-LINES (1) FILE
         LR    R11,R4             SAVE REG 4
         SR    R5,R5
         SR    R6,R6
         CLI   WKUPDOW,C'U'       DO WE SEARCH BACKWARDS
         BE    SEARUP             YES, WE DO
         CP    WKSTALN,ZERO       DO WE HAVE A STARTING LINE NUMBER
         BE    CHECKCOL           NO, THEN ALL IS OK
         SP    WKSTALN,ONE        MAKE IT 1 LESS FOR MATH REASONS
CHECKCOL CP    WKSTACO,ZERO       DO WE HAVE A STARTING COLUMN
         BNE   FINDQT             BRANCH IF YES
         ZAP   WKSTACO,ONE        MAKE IT 1
         B     FINDQT             JUMP
SEARUP   CP    WKSTALN,ZERO       DO WE HAVE A STARTING LINE NUMBER
         BNE   DOLASTLN           YES, THEN USE IT
         ZAP   WKSTALN,WKTOTLN    MAKE IT THE LAST LINE ON PAGE
DOLASTLN SP    WKSTALN,ONE        MAKE IT 1 LESS FOR MATH REASONS
         CP    WKSTACO,ZERO       DO WE HAVE A STARTING COLUMN
         BNE   FINDQT             BRANCH IF YES
ZAPUP    ZAP   WKSTACO,N133       MAKE IT 133
FINDQT   LA    R5,SEARARG         GET ADDRESS OF SEARCH ARGUMENT
         LA    R6,55              LOAD FIELD LENGTH
CHKQT    CLI   0(R5),X'7D'        DO WE HAVE A SINGLE QUOTE
         BE    GOTQS              YES, WE GOT ONE
         CLI   0(R5),X'7F'        DO WE HAVE A DOUBLE QUOTE
         BE    GOTQD              YES, WE GOT ONE
         CLI   0(R5),X'4D'        DO WE HAVE A LEFT PARENTHESIS
         BE    GOTQP              YES, WE GOT ONE
         LA    R5,1(R5)           BUMP TO NEXT POSITION
         BCT   R6,CHKQT           SUBTRACT 1 FROM R6 AND BRANCH
         MVI   WKGOTIT,C'S'       NO QUOTE FOUND BAD SEARCH ARGUMENT
         B     EXIT               THEN GET OUT NO CAN SEARCH IT
GOTQS    MVI   QUOTE,X'7D'        USE SINGLE QUOTE
         LR    R7,R5              SAVE STARTING POSTION OF QUOTE
         B     CHECKLQ
GOTQP    MVI   QUOTE,X'5D'        USE RIGHT PARENTHESIS
         LR    R7,R5              SAVE STARTING POSTION
         B     CHECKLQ
GOTQD    MVI   QUOTE,X'7F'        USE DOUBLE QUOTE
         LR    R7,R5              SAVE STARTING POSTION OF QUOTE
CHECKLQ  LA    R5,1(R5)           BUMP TO NEXT POSITION
         CLC   0(1,R5),QUOTE      DO WE HAVE OUR NEXT QUOTE
         BE    GOTLQ              YES, WE GOT ONE
         BCT   R6,CHECKLQ         SUBTRACT 1 FROM R6 AND BRANCH
         MVI   WKGOTIT,C'E'       NO QUOTE FOUND BAD SEARCH ARGUMENT
         B     EXIT               THEN GET OUT NO CAN SEARCH IT
GOTLQ    LA    R7,1(R7)           BUMP PAST QUOTE
         SR    R5,R7              SUBTRACT STARTING AND ENDING
         CVD   R5,DB              CONVERT TO PACKED
         ZAP   LENGTH,DB          SAVE THIS LENGTH
         BCTR  R5,0               SUBTRACT 1 FROM RESULT
         CLI   WKUPDOW,C'U'       DO WE SEARCH BACKWARDS
         BE    SEARUP1            YES, WE DO
GETADDR  ZAP   STARTCO,N133       GET REMAINING LENGTH OF LINE
         SP    STARTCO,WKSTACO
         CP    STARTCO,LENGTH     LENGTH STR LONGER-REMAINING LENGTH
         BL    NEXTLINE           MOVE TO NEXT LINE
         SR    R8,R8              CLEAR R8
         L     R9,=F'133'         LOAD LENGTH OF LINE
         ZAP   DB,WKSTALN         CONVERT       LINE NUMBER
         CVB   R6,DB                 TO HEX
         MR    R8,R6              GET DIPLACEMENT
         LR    R4,R11             RELOAD REG 4 WITH START OF RECORD
         AR    R4,R9              ADD TO ADDRESS OF SYSOUT LINE
         ZAP   DB,WKSTACO         CONVERT      COLUMN NUMBER
         CVB   R6,DB                 TO HEX
         AR    R4,R6              ADD TO ADDRESS OF SYSOUT LINE
         EX    R5,EXCLC1
*        LR    R6,R7              LOAD ADDRESS OF SEARCH ARG
*        LR    R7,R5              MAKE COMPARE LONGS THE SAME LENGTH
*        CLCL  R6,R4              COMPARE DETAIL RECORD WITH STRING
         BE    GOTIT              WE GOT A HIT
         AP    WKSTACO,ONE        ADD 1 TO COLUMN NUMBER
         B     GETADDR            LOOK AGAIN
NEXTLINE AP    WKSTALN,ONE        THEN GET OUT
         ZAP   WKSTACO,ONE        START COLUMN OVER AT ONE
         CP    WKTOTLN,WKSTALN    IS THIS THE LAST LINE
         BL    NOGOTIT            BUMP TO NEXT SYSOUT LINE
         B     GETADDR            TRY AGAIN
SEARUP1  ZAP   STARTCO,LENGTH     GET REMAINING LENGTH OF LINE
         AP    STARTCO,WKSTACO    ADD STARTING COLUMN NUMBER
         CP    STARTCO,N133       PAST THE STARTING POINT FOR THIS LINE
         BNH   LOOKUP             JUMP IF NO NEED TO RESET COLUMN
         ZAP   WKSTACO,N133       SET COLUMN NUMBER TO END
         SP    WKSTACO,LENGTH     SET TO LAST POSSIBLE HIT
LOOKUP   SR    R8,R8              CLEAR R8
         L     R9,=F'133'         LOAD LENGTH OF LINE
         ZAP   DB,WKSTALN         CONVERT       LINE NUMBER
         CVB   R6,DB                 TO HEX
         MR    R8,R6              GET DIPLACEMENT
         LR    R4,R11             RELOAD REG 4 WITH START OF RECORD
         AR    R4,R9              ADD TO ADDRESS OF SYSOUT LINE
         ZAP   DB,WKSTACO         CONVERT      COLUMN NUMBER
         CVB   R6,DB                 TO HEX
         AR    R4,R6              ADD TO ADDRESS OF SYSOUT LINE
         EX    R5,EXCLC1
*        LR    R6,R7              LOAD ADDRESS OF SEARCH ARG
*        LR    R7,R5              MAKE COMPARE LENGTHS THE SAME
*        CLCL  R6,R4              COMPARE DETAIL RECORD WITH STRING
         BE    GOTIT              WE GOT A HIT
         SP    WKSTACO,ONE        SUBTRACT 1 FROM COLUMN NUMBER
         CP    WKSTACO,ZERO       ARE WE AT THE FRONT OF THE LINE
         BE    NEXTUP             YES, GO TO NEXT LINE
         B     SEARUP1            LOOK AGAIN
NEXTUP   SP    WKSTALN,ONE        THEN GET OUT
         BM    NOGOTIT            BRANCH IF NEGATIVE,  NO HIT
         ZAP   WKSTACO,N133       SET COLUMN NUMBER TO END
         SP    WKSTACO,LENGTH     SET TO LAST POSSIBLE HIT
         B     SEARUP1            TRY AGAIN
NOGOTIT  MVI   WKGOTIT,C'N'       NO HIT ON THIS PAGE
         B     EXIT               THEN GET OUT NO CAN SEARCH IT
GOTIT    MVI   WKGOTIT,C'Y'       TELL COBOL PROGRAM WE GOT A HIT
         EJECT
         SPACE
EXIT     DS    0H
         XR    R15,R15
         RETURN (14,12)
EXCLC1   CLC   0(0,R7),0(R4)      COMPARE DETAIL RECORD WITH STRING
         EJECT
*----------------------------------------------------------------------
* LITERALS FOR BASE1 AND BASE2 ----------------------------------------
*----------------------------------------------------------------------
ONE      DC    PL1'1'
ZERO     DC    PL1'0'
N133     DC    PL2'133'
         SPACE
         LTORG
         EJECT
*----------------------------------------------------------------------
* EQUATES FOR PROGRAM -------------------------------------------------
*----------------------------------------------------------------------
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*----------------------------------------------------------------------
* PARAMETERS FROM COBOL PROGRAMS  -------------------------------------
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
* $$NOTE$$  DO NOT CHANGE THE ORDER OF THIS DSECT.    $$NOTE$$
* $$NOTE$$  ANY CHANGE OF ANY KIND TO THIS DSECT      $$NOTE$$
* $$NOTE$$  MUST ALSO BE MADE TO THE CICS COBOL       $$NOTE$$
* $$NOTE$$  PROGRAM.  THE FIELDS IN THE COBOL PROGRAM $$NOTE$$
* $$NOTE$$  SHOULD BE THE SAME SIZE AS THESE AND      $$NOTE$$
* $$NOTE$$  ALSO THE SAME ORDER.                      $$NOTE$$
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*----------------------------------------------------------------------
PARM1    DSECT
WKSTALN  DS    PL2      STARTING LINE NUMBER
WKSTACO  DS    PL2      STARTING COLUMN NUMBER
WKUPDOW  DS    CL1      WHICH DIRECTION UP OR DOWN
WKGOTIT  DS    CL1      HIT/ERROR FLAG
SEARARG  DS    CL55     SEARCH FIELD FROM MAP
WKTOTLN  DS    PL2      TOTAL LINES IN DETAIL RECORD
WKMAXLN  DS    PL2      MAX LINES TO DISPLAY
WKCOLNM  DS    PL2      STARTING COLUMN TO DISPLAY
WKLNENM  DS    PL2      STARTING LINE NUMBER TO DISPLAY
WKLNELG  DS    PL2      LINE LENGTH TO DISPLAY
WKSCREN  DS    PL2      SCREEN SIZE MODEL 2 OR 5
WKAREA   DS    0CL40    WORK AREA USED BY THIS PROGRAM
DB       DS    D
QUOTE    DS    CL1
LENGTH   DS    PL2
STARTCO  DS    PL2
*----------------------------------------------------------------------
*- DETAIL RECORD ------------------------------------------------------
*----------------------------------------------------------------------
PARM2    DSECT
WKRECLN  DS    CL133
         END
